;	R11		
;       CRAZY-FIRE   V1  FOR PC-8801 WITH BCF
;          BY AKIE
;--------------------------------------------------------------
.Z80
;
;
;
VRMSIZ	EQU	4000H	
SUBSIZ	EQU	2048+1024	;64*48
CHRGEN	EQU	4	
VRAM1	EQU	5CH	
VRAM2	EQU	5DH	
INDEX	EQU	0E000H	
SUBSCR	EQU	INDEX-SUBSIZ	
XTBL	EQU	SUBSCR-1024	
WK1	EQU	XTBL-4	
CNTS	EQU	XTBL-2	
KYORI	EQU	XTBL-10	
KIRFLG	EQU	XTBL-12	
BASFLG	EQU	XTBL-13	
INTFLG	EQU	XTBL-14	
UMSLF	EQU	XTBL-15	
UMSLA	EQU	XTBL-17	
DMSLF	EQU	XTBL-18	
DMSLA	EQU	XTBL-20	
;
;
;
;
;
GRWORK	EQU	0A003H	
CHRBUF	EQU	GRWORK	
SPKEEP	EQU	GRWORK+32	
BACKP1	EQU	GRWORK+34	
BACKP2	EQU	GRWORK+36	
CHIPAT	EQU	GRWORK+38	
;
;
;
STACK	EQU	0B100H	;SUBSCR-0
STEP	EQU	32	
IDCODE	EQU	0	
SCRAL	EQU	1	
SCRAH	EQU	2	
SUBAL	EQU	3	
SUBAH	EQU	4	
YPOS	EQU	5	
XPOS	EQU	6	
SYPOS	EQU	7	
SXPOS	EQU	8	
DX	EQU	9	
DY	EQU	10	
REPCON	EQU	11	
PATAL	EQU	12	
PATAH	EQU	13	
SMOKAL	EQU	14	
SMOKAH	EQU	15	
;
FORCOL	EQU	7	
BAKCOL	EQU	1	
MESCOL	EQU	5	
;
;
START:			
	DI		
	LD	SP,STACK	
;
	LD	A,(0A100H)	
	CP	"c"	
	JR	NZ,SEC10	
	LD	A,(0A101H)	
	CP	"f"	
	JR	Z,SEC15	
SEC10:			
	LD	A,6	
	OUT	(31H),A	
	LD	HL,7000H	
	LD	DE,0A100H	
	LD	BC,1000H	
	LDIR		
	LD	A,4	
	OUT	(31H),A	
SEC15:			
;
; SCREEN INIT
;
;I/O PORT INIT
;STOP TEXT SCREEN
	XOR	A	
	OUT	(81),A	
;
	LD	A,1CH	
	OUT	(31H),A	
	LD	A,11H	
	OUT	(40H),A	
;
;BORDA COLOR
	XOR	A	
	OUT	(52H),A	
;
;PALLET ALL BLACK
	LD	C,54H	
	LD	B,8	
	XOR	A	
SEC20:			
	OUT	(C),A	
	INC	C	
	DJNZ	SEC20	
;
	CALL	ALLCLS	
	CALL	INITSG	
;
	LD	HL,SUBSCR	
	LD	DE,SUBSCR+1	
	LD	BC,SUBSIZ-1	
	LD	(HL),0	
	LDIR		
;
;
	LD	IX,XTBL	
	LD	(IX+IDCODE),2	
;SCRA,SUBA
;
	LD	(IX+YPOS),40	
	LD	(IX+XPOS),50	
	LD	(IX+REPCON),0	
	LD	HL,(0A104H+6)	
	LD	(IX+PATAL),L	
	LD	(IX+PATAH),H	
	LD	(IX+PATAL+16),L	
	LD	(IX+PATAH+16),H	
;
;
	LD	B,30	
	LD	DE,STEP	
SEC50:	ADD	IX,DE	
	LD	(IX+0),0	
	DJNZ	SEC50	
	LD	(IX+STEP),0FFH	
;
	XOR	A	
	LD	(UMSLF),A	
	LD	(DMSLF),A	
	LD	HL,SMTBLU	
	LD	(UMSLA),HL	
	LD	HL,SMTBLD	
	LD	(DMSLA),HL	
;
;
	CALL	CLDSTN	
	LD	A,0	
	LD	(KIRFLG),A	
	LD	A,0	
	LD	(INTFLG),A	
;
	CALL	SETCOL	
	XOR	A	
	LD	(BACKP1),A	
	LD	(BACKP1+1),A	
	LD	A,0FFH	
	LD	(BACKP2),A	
	LD	A,0FFH	
	LD	(BACKP2+1),A	
;
	LD	IX,CHIPAT	
	LD	(IX+0),33H	
	LD	(IX+1),0CCH	
	LD	(IX+2),33H	
	LD	(IX+3),0CCH	
	LD	(IX+4),0FFH	
	LD	(IX+5),0FFH	
	LD	(IX+6),0FFH	
	LD	(IX+7),0FFH	
;
	CALL	GRCLR	
;
	LD	BC,8000H	
	CALL	SETCUR	
	LD	HL,MSG1	
	CALL	PUTLIN	
	LD	BC,8208H	
	CALL	SETCUR	
	LD	HL,MSG2	
	CALL	PUTLIN	
	LD	BC,7C18H	
	CALL	SETCUR	
	LD	HL,MSG3	
	CALL	PUTLIN	
;
MA:			
;
	CALL	SCROLL	
	CALL	CHIK	
	CALL	SMOKEM	
;CALL MAKINT
;CALL MAKKIR
	CALL	SHPMSL	
	CALL	CHARAS	
	CALL	BOOMM	
	CALL	COUNT	
;
	CALL	SCRGR	
	CALL	DSPCHR	
;
	CALL	DSPCHI	
	CALL	DSPCNT	
;
;
;CALL 0F75H
	IN	A,(9)	
	INC	A	
	JR	Z,MA	
ESC:			
;
	OUT	(5FH),A	
;
;	LD	A,19H	
;	CALL	9D7H	
	JP	0H	;WARM BOOT
;
SCROLL:			
	LD	DE,SUBSCR	
	LD	HL,SUBSCR+1	
	LD	BC,SUBSIZ-1	
	LDIR		
	LD	HL,SUBSCR+63	
	LD	DE,64	
	LD	B,48	
	LD	A,0	
SCL10:			
	LD	(HL),A	
	ADD	HL,DE	
	DJNZ	SCL10	
;
	LD	IX,XTBL	
SCL50:	LD	A,(IX+IDCODE)	
	OR	A	
	JR	Z,SCLEE	
	INC	A	
	RET	Z	
;
	LD	B,2	
SCL55:			
;DEC (IX+SCRA)
;JR NZ,SCL60
;DEC (IX+SCRA+1)
;SCL60:
;DEC (IX+SUBA)
;JR NZ,SCL70
;DEC (IX+SUBA+1)
;SCL70:
	DEC	(IX+SXPOS)	
	DEC	(IX+SXPOS)	
	LD	A,(IX+XPOS)	
	SUB	2	
	LD	(IX+XPOS),A	
	JR	C,SCLE1	
	LD	A,(IX+XPOS+16)	
	SUB	2	
	LD	(IX+XPOS+16),A	
	JR	C,SCLE1	
;DJNZ SCL55
	JR	SCLEE	
SCLE1:			
	LD	(IX+IDCODE),0	
SCLEE:			
	LD	DE,STEP	
	ADD	IX,DE	
	JR	SCL50	
;
SCRGR:			
	DEFS	1501-3	
	OUT	(5FH),A	
	RET		
;
INITSG:			
	LD	HL,SCRGR	
	LD	BC,136	
	LD	DE,0C000H+644	
	LD	(HL),0D3H	
	INC	HL	
	LD	(HL),5DH	
	INC	HL	
;
ISG100:			
	LD	A,5DH	
	CALL	ISG300	
;LD A,5EH
;CALL ISG300
	PUSH	HL	
	LD	HL,80	
	ADD	HL,DE	
	EX	DE,HL	
	POP	HL	
	DEC	BC	
	LD	A,B	
	OR	C	
	JR	NZ,ISG100	
	LD	(HL),0D3H	
	INC	HL	
	LD	(HL),5FH	
	INC	HL	
	LD	(HL),0C9H	
	RET		
;
ISG300:			
;LD (HL),0D3H
;INC HL
;LD (HL),A
;INC HL
	LD	(HL),21H	
	INC	HL	
	INC	DE	
	LD	(HL),E	
	INC	HL	
	LD	(HL),D	
	INC	HL	
	LD	(HL),11H	
	INC	HL	
	DEC	DE	
	LD	(HL),E	
	INC	HL	
	LD	(HL),D	
	INC	HL	
	LD	(HL),1H	
	INC	HL	
	LD	(HL),59	
	INC	HL	
	LD	(HL),0	
	INC	HL	
	LD	(HL),0EDH	
	INC	HL	
	LD	(HL),0B0H	
	INC	HL	
	RET		
;
GRCLR:			
	LD	C,5DH	
	LD	A,(BACKP1)	
	LD	L,A	
	LD	H,A	
	CALL	GRCLR1	
	LD	A,(BACKP1+1)	
	LD	L,A	
	LD	H,A	
	CALL	GRCLR2	
	LD	C,5EH	
	LD	A,(BACKP2)	
	LD	L,A	
	LD	H,A	
	CALL	GRCLR1	
	LD	A,(BACKP2+1)	
	LD	L,A	
	LD	H,A	
	CALL	GRCLR2	
	RET		
;
;C REG NI PAGE ADR WO IRERU
GRCLR1:			
	LD	(SPKEEP),SP	
	OUT	(C),A	
	LD	SP,0C000H+11424	
	LD	B,68	
	JR	SCLR10	
GRCLR2:			
	LD	(SPKEEP),SP	
	OUT	(C),A	
	LD	SP,0C000H+11504	
	LD	B,68	
SCLR10:			
;   00112233445566778899
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	LD	IX,0-100	
	ADD	IX,SP	
	LD	SP,IX	
	DJNZ	SCLR10	
	OUT	(5FH),A	
	LD	SP,(SPKEEP)	
	RET		
;
BANKP1:			
	OUT	(5DH),A	
	RET		
BANKP2:			
	OUT	(5EH),A	
	RET		
BANKMS:			
	OUT	(5CH),A	
	RET		
BANKM:			
	OUT	(5FH),A	
	RET		
;
SETCOL:			
	XOR	A	
	OUT	(54H),A	
	LD	C,55H	
	LD	A,1	
	LD	B,7	
;
STC1:			
	OUT	(C),A	
	INC	A	
	INC	C	
	DJNZ	STC1	
;
	LD	A,1	
	OUT	(58H),A	
	LD	A,3	
	OUT	(5AH),A	
;
	RET		
;
ALLCLS:			
	LD	(SPKEEP),SP	
	LD	C,5CH	
CLS05:			
	OUT	(C),A	
	LD	SP,0FE80H	
	LD	B,100	
	LD	HL,0	
CLS10:			
;   0011223344556677889900112233445566778899
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DEFB	0E5H,0E5H,0E5H,0E5H,0E5H
	DJNZ	CLS10	
	INC	C	
	LD	A,C	
	CP	5FH	
	JR	NZ,CLS05	
	OUT	(5FH),A	
	LD	SP,(SPKEEP)	
	RET		
;
DSPCHI:			
	LD	B,128-6	
	LD	C,68	
	CALL	DRADR	
	DEC	C	
	DEC	C	
	CALL	SUBADR	
;
	OUT	(5DH),A	
DCH100:			
	OUT	(5FH),A	
	LD	A,(IY+0)	
	OUT	(5DH),A	
	CP	7FH	
	JR	NZ,DCH400	
	LD	DE,CHIPAT	
	LD	B,4	
DCH200:			
	PUSH	DE	
	LD	DE,0-80	
	ADD	HL,DE	
	POP	DE	
	LD	A,(DE)	
	LD	(HL),A	
	INC	DE	
	DJNZ	DCH200	
	LD	DE,0-64	
	ADD	IY,DE	
	JR	DCH100	
DCH400:			
	LD	DE,0-80	
	LD	BC,(BACKP1)	
	ADD	HL,DE	
	LD	(HL),B	
	ADD	HL,DE	
	LD	(HL),C	
	ADD	HL,DE	
	LD	(HL),B	
	ADD	HL,DE	
	LD	(HL),C	
	OUT	(5FH),A	
	RET		
;
PUTCHR:			
	PUSH	AF	
	PUSH	HL	
	PUSH	DE	
	PUSH	BC	
;
	CP	20H	
	JR	C,PCH10	
	CP	5AH	
	JR	NC,PCH10	
	CP	30H	
	JR	C,PCH20	
PCH10:			
	LD	L,A	
	LD	H,0	
	LD	DE,2300H	
	ADD	HL,DE	
	JR	PCH30	
PCH20:			
	SUB	20H	
	ADD	A,A	
	LD	L,A	
	LD	H,0	
	LD	DE,ASCJIS	
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
PCH30:			
	LD	A,H	
	AND	7	
	LD	H,A	
	LD	A,L	
	AND	60H	
	RRCA		
	RRCA		
	OR	H	
	LD	H,A	
	LD	A,L	
	ADD	A,A	
	ADD	A,A	
	ADD	A,A	
	LD	L,A	
	ADD	HL,HL	
	LD	B,10H	
	LD	DE,CHRBUF	
PCH100:			
	LD	A,L	
	OUT	(0E8H),A	
	LD	A,H	
	OUT	(0E9H),A	
	OUT	(0EAH),A	
	NOP		
	NOP		
	IN	A,(0E9H)	
	LD	(DE),A	
	INC	E		
	IN	A,(0E8H)	
	LD	(DE),A	
	INC	DE	
	OUT	(0EBH),A	
	INC	L	
	DJNZ	PCH100	
	LD	DE,CHRBUF	
	LD	A,(CURX)	
	LD	B,A	
	LD	A,(CURY)	
	LD	C,A	
	CALL	DRADR	
	LD	BC,80-1	
	OUT	(5CH),A	
	LD	A,10H	
PCH200:			
	EX	AF,AF'	
	LD	A,(DE)	
	LD	(HL),A	
	INC	DE	
	INC	HL	
	LD	A,(DE)	
	LD	(HL),A	
	EX	AF,AF'	
	INC	DE	
	ADD	HL,BC	
	DEC	A	
	JR	NZ,PCH200	
	OUT	(5FH),A	
	LD	A,(CURX)	
	ADD	A,4	
	LD	(CURX),A	
	POP	BC	
	POP	DE	
	POP	HL	
	POP	AF	
	RET		
;
;DISPXX  HL:DRADR,IY:PATTERNADR
;               BC:XY COUNT
DISPXX:			
	OUT	(5DH),A	
	PUSH	HL	
DXX100:			
	PUSH	BC	
	PUSH	HL	
DXX200:			
	LD	A,(HL)	
	AND	(IY+0)	
	OR	(IY+1)	
	LD	(HL),A	
	INC	HL	
	INC	IY	
	INC	IY	
	DJNZ	DXX200	
	POP	HL	
	LD	BC,80	
	ADD	HL,BC	
	POP	BC	
	DEC	C	
	JR	NZ,DXX100	
	OUT	(5FH),A	
	POP	HL	
	RET		
;
CLRSXX:			
	PUSH	BC	
	PUSH	HL	
	OUT	(5DH),A	
CXX100:			
	PUSH	BC	
	PUSH	HL	
CXX200:			
	LD	A,(HL)	
	AND	(IY+0)	
	LD	(HL),A	
	INC	HL	
	INC	IY	
	INC	IY	
	DJNZ	CXX200	
	POP	HL	
	LD	BC,80	
	ADD	HL,BC	
	POP	BC	
	DEC	C	
	JR	NZ,CXX100	
	OUT	(5FH),A	
	POP	HL	
	POP	BC	
	RET		
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
;
; FROM HERE ,NEED NOT OPEN GRAPHIC PAGE
;
MSG1:	DEFM	"CRAZY",00	
MSG2:	DEFM	" FIRE",00	
MSG3:	DEFM	"DISTANCE",00	
;
;
CHIK:			
	LD	A,(CHIUND)	
	CP	64	
	JR	NC,CHIUP	
	CP	45	
;CP 5
	JR	C,CHIDWN	
	CALL	RND	
	AND	1	
	JR	Z,CHIDWN	
CHIUP:			
	LD	A,(CHIUND)	
	SUB	2	
	JR	CHICC	
CHIDWN:			
	LD	A,(CHIUND)	
	ADD	A,2	
CHICC:			
	LD	(CHIUND),A	
;
	LD	B,127	
	LD	C,96-2	
	CALL	SUBADR	
	LD	DE,0-64	
CHI100:			
	LD	(IY+0),7FH	
	DEC	C	
	DEC	C	
	ADD	IY,DE	
	LD	A,(CHIUND)	
	CP	C	
	JR	C,CHI100	
	RET		
;
CHIUND:	DEFB	50	
;CHIUND:DB 68
CHFLG:	DEFB	0	
;
CHARAS:			
	LD	IX,XTBL	
CHR10:			
	LD	A,(IX+IDCODE)	
	OR	A	
	JR	Z,CHRE	
	CP	0FFH	
	RET	Z	
	LD	HL,CHRTBL	
	LD	E,A	
	LD	D,0	
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	LD	HL,CHRE	
	PUSH	HL	
	EX	DE,HL	
	JP	(HL)	
CHRE:	LD	DE,STEP	
	ADD	IX,DE	
	JR	CHR10	
CHRTBL:			
	DEFW	ERROR	
	DEFW	SHPCON	
	DEFW	MISSIL	
	DEFW	KIRAI	
	DEFW	BASE	
	DEFW	INTERS	
	DEFW	DESTRO	
	DEFW	CRUISE	
	DEFW	BATLSP	
	DEFW	RETURN	;SMOKE
	DEFW	RETURN	;BOOM
;
KIRAI:			
INTERS:			
BASE:			
DESTRO:			
CRUISE:			
BATLSP:			
ERROR:	LD	HL,ERRMES	
	CALL	PRSTR0	
	JP	ESC	
ERRMES:	DEFM	0DH,0AH,"Undefine charactor exist!",07,00	
;
RETURN:	RET		
;
MISSIL:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	LD	(IX+XPOS+16),B	
	LD	(IX+YPOS+16),C	
	PUSH	BC	
	LD	B,0	
	LD	A,(IX+IDCODE)	
	CALL	MSUB	
	POP	BC	
;JR NZ,MISBOM
	CALL	SUBHL	
	LD	A,(IX+REPCON)	
	LD	D,(IX+DX)	
	LD	E,(IX+DY)	
MSL100:			
	EX	AF,AF'	
	PUSH	HL	
	PUSH	BC	
	LD	A,B	
	ADD	A,(IX+DX+16)	
	LD	B,A	
	LD	A,C	
	ADD	A,(IX+DY+16)	
	LD	C,A	
	CALL	SUBHL	
	LD	A,(HL)	
	OR	A	
	JR	Z,MSL120	
	BIT	7,A	
	JR	Z,MSL140	
MSL120:			
	CALL	RND	
	AND	7	
	ADD	A,85H	
	LD	(HL),A	
MSL140:			
	POP	BC	
	POP	HL	
;
	CALL	DXDY	
	JR	NC,MSL900	
	LD	A,(HL)	
	OR	A	
	JR	Z,MSL200	
	AND	80H	
	JR	Z,MISBOM	
MSL200:			
	EX	AF,AF'	
	DEC	A	
	JR	NZ,MSL100	
	LD	(IX+XPOS),B	
	LD	(IX+YPOS),C	
	LD	(IX+SUBAL),L	
	LD	(IX+SUBAH),H	
	LD	A,0	
	LD	B,(IX+IDCODE)	
	CALL	MSUB	
	JR	NZ,MISBOM	
	RET		
MSL900:			
	LD	(IX+IDCODE+16),0	
	RET		
;
MISBOM:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	LD	A,C	
	SUB	3	
	LD	C,A	
	LD	A,B	
	SUB	3	
	LD	B,A	
	CALL	BOOMS	
;
;
	JP	MSL900	
;
; DXDY SUB:[SCREEN POS(IY)],SUB POS(HL), X(B),Y(C) Æ DX(D),DY(E) ¦ ¸Ü´Ù
;  IF NC THEN DXDYERROR
DXDY:			
	LD	A,E	
	OR	A	
	JR	Z,DXDY20	
	CP	1	
	JR	Z,ADD120	
SUB120:			
	BIT	0,C	
	JR	NZ,DXDY20	
	PUSH	DE	
	LD	DE,0-64	
	ADD	HL,DE	
	POP	DE	
	JR	DXDY20	
ADD120:			
	BIT	0,C	
	JR	Z,DXDY20	
	PUSH	DE	
	LD	DE,64	
	ADD	HL,DE	
	POP	DE	
DXDY20:			
	ADD	A,C	
	LD	C,A	
	CP	68	
	JR	NC,DXDYER	
	OR	A	
	JR	Z,DXDYER	
	LD	A,D	
	OR	A	
	JR	Z,DXDY30	
	CP	1	
	JR	Z,ADD1	
SUB1:			
	BIT	0,B	
	JR	NZ,DXDY30	
	DEC	HL	
	JR	DXDY30	
ADD1:			
	BIT	0,B	
	JR	Z,DXDY30	
	INC	HL	
DXDY30:			
	ADD	A,B	
	LD	B,A	
	OR	A	
	JR	Z,DXDYER	
;BIT 7,A
;JR NZ,DXDYER
	CP	4	
	JR	C,DXDYER	
;CP 124
	CP	122	
	JR	NC,DXDYER	
	RET		
DXDYER:	OR	A	
	RET		
;
FDXDY:			
	PUSH	DE	
	LD	D,(IX+DX)	
	LD	E,(IX+DY)	
	CALL	DXDY	
	POP	DE	
	RET		
;
RND:			
	PUSH	HL	
	LD	H,44H	
	LD	A,R	
	ADD	A,L	
	LD	L,A	
	LD	A,(HL)	
;
	LD	(LSTRND),A	
	POP	HL	
	RET		
;
LSTRND:	DEFS	1	
;
DRADR:			
	PUSH	DE	
	LD	L,C	
	INC	L	
	INC	L	
	INC	L	
	INC	L	
	LD	H,0	
	SLA	L	
	LD	E,L	
	LD	D,H	
	ADD	HL,HL	
	ADD	HL,HL	
	ADD	HL,DE	
	ADD	HL,HL	
;
	ADD	HL,HL	
	ADD	HL,HL	
	ADD	HL,HL	
	LD	E,B	
	SRL	E	
	LD	D,0	
	ADD	HL,DE	
	LD	DE,0C002H	
	ADD	HL,DE	
	POP	DE	
	RET		
;
SUBADR:			
	PUSH	AF	
	PUSH	HL	
	LD	IY,SUBSCR	
	LD	H,C	
	LD	L,0	
	RES	0,H	
	SRL	H	
	RR	L	
	SRL	H	
	RR	L	
	SRL	H	
	RR	L	
	EX	DE,HL	
	ADD	IY,DE	
SUBA10:			
	LD	D,0	
	LD	E,B	
	SRL	E	
	ADD	IY,DE	
	EX	DE,HL	
	POP	HL	
	POP	AF	
	RET		
;
SUBHL:			
	PUSH	IY	
	CALL	SUBADR	
	PUSH	IY	
	POP	HL	
	POP	IY	
	RET		
;
DSPCHR:			
	LD	IX,XTBL	
DCR100:			
	LD	A,(IX+IDCODE)	
	OR	A	
	JR	Z,DCR900	
	CP	0FFH	
	RET	Z	
	LD	E,A	
	LD	D,0	
	LD	HL,DCRTBL	
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
	LD	DE,DCR900	
	PUSH	DE	
	JP	(HL)	
DCR900:			
	LD	DE,STEP	
	ADD	IX,DE	
	JR	DCR100	
DCRTBL:			
	DEFW	ERROR	
	DEFW	SHIPDS	
	DEFW	MSLDS	
	DEFW	KIRDS	
	DEFW	BASEDS	
	DEFW	INTDS	
	DEFW	DSTDS	
	DEFW	CRSDS	
	DEFW	BTLDS	
	DEFW	SMOKDS	
	DEFW	BOOMDS	
KIRDS:			
BASEDS:			
INTDS:			
DSTDS:			
CRSDS:			
BTLDS:			
	JP	ERROR	
;
SHIPDS:			
	LD	IX,XTBL	
	LD	A,(IX+IDCODE)	
	CP	2	
	RET	NZ	
	LD	A,(IX+YPOS)	
	CP	(IX+YPOS+16)	
	JR	NZ,SHDS10	
	CALL	SETSH	
	CALL	DISPXX	
	EX	AF,AF'	
	OR	A	
	RET		
SHDS10:			
	PUSH	IX	
	LD	BC,16	
	ADD	IX,BC	
	CALL	SETSH	
	CALL	CLRSXX	
	POP	IX	
	CALL	SETSH	
	CALL	DISPXX	
	RET		
;
SETSH:			
	LD	A,(IX+REPCON)	
	OR	A	
	LD	IY,(0A104H+2)	
	JR	Z,SETSH1	
	LD	IY,(0A104H+4)	
SETSH1:			
	LD	C,(IX+YPOS)	
	LD	B,(IX+XPOS)	
	CALL	DRADR	
	LD	B,4	
	LD	C,10	
	RET		
;
CALCAD:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	CALL	DRADR	
	RET		
;
DSET:			
	CALL	CALCAD	
	PUSH	HL	
	LD	L,(IX+PATAL)	
	LD	H,(IX+PATAH)	
	BIT	0,B	
	JR	NZ,DSET10	
	INC	HL	
	INC	HL	
	JR	DSET20	
DSET10:			
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
DSET20:			
	LD	B,(HL)	
	INC	HL	
	LD	C,(HL)	
	INC	HL	
	PUSH	HL	
	POP	IY	
	POP	HL	
	RET		
;
;
MSLDS:			
	PUSH	IX	
	LD	DE,16	
	ADD	IX,DE	
	CALL	DSET	
	CALL	CLRSXX	
	LD	A,(IX+IDCODE)	
	POP	IX	
	OR	A	
	JR	Z,MSLDS1	
	LD	A,(IX+IDCODE)	
	CP	4	
	CALL	Z,GENSMK	
	CALL	DSET	
	CALL	DISPXX	
	RET		
MSLDS1:			
	CALL	SMOKEN	
	RET		
;
GENSMK:			
	LD	B,(IX+REPCON)	
GSM50:			
	PUSH	BC	
	LD	A,(IX+XPOS+16)	
	ADD	A,(IX+DX+16)	
	LD	B,A	
	LD	A,(IX+YPOS+16)	
	ADD	A,(IX+DY+16)	
	LD	C,A	
	LD	L,(IX+SMOKAL)	
	LD	H,(IX+SMOKAH)	
	CALL	SMKPTN	
	JR	GSM300	
;
;
SMKPTN:			
	CALL	SETPAD	
	LD	D,(HL)	
	INC	HL	
	LD	E,(HL)	
	INC	HL	
	PUSH	DE	
	LD	A,(IX+REPCON+16)	
	CP	55H	
	JR	Z,GSM100	
	OR	A	
;JR Z,GSM100
	INC	HL	
	INC	HL	
	INC	HL	
	INC	HL	
	LD	A,D	
	CP	1	
	JR	Z,GSM100	
	INC	HL	
	INC	HL	
	INC	HL	
	INC	HL	
GSM100:			
	PUSH	HL	
	POP	IY	
	CALL	DRADR	
	POP	BC	
	LD	C,2	
	RET		
;
;
;
GSM300:			
	CALL	DISPXX	
	LD	A,(IX+XPOS+16)	
	ADD	A,(IX+DX)	
	LD	(IX+XPOS+16),A	
	LD	A,(IX+YPOS+16)	
	ADD	A,(IX+DY)	
	LD	(IX+YPOS+16),A	
	LD	A,(IX+REPCON+16)	
	CP	55H	
	JR	Z,GSM400	
	CPL		
	LD	(IX+REPCON+16),A	
GSM400:			
	POP	BC	
	DJNZ	GSM50	
	RET		
;
SHPCON:			
	LD	IX,XTBL	
	LD	A,(IX+XPOS)	
	LD	(IX+XPOS+16),A	
	LD	A,(IX+YPOS)	
	LD	(IX+YPOS+16),A	
	INC	(IX+XPOS)	
	INC	(IX+XPOS)	
	LD	A,(IX+IDCODE)	
	CP	2	
	RET	NZ	
	LD	A,(REPCON)	
	CPL		
	LD	(REPCON),A	
;
	IN	A,(1)	
	XOR	0FFH	
	JR	Z,SCON20	
	LD	A,(IX+YPOS)	
	OR	A	
	JR	Z,SCON20	
;
	DEC	(IX+YPOS)	
;
SCON20:	IN	A,(0)	
	XOR	0FFH	
	JR	Z,SCON50	
	LD	A,(IX+YPOS)	
	CP	60	
	JR	NC,SCON50	
;
	INC	(IX+YPOS)	
SCON50:			
	LD	A,(IX+IDCODE)	
	PUSH	IX	
	LD	BC,16	
	ADD	IX,BC	
	LD	B,0	
	CALL	SCONS	
	POP	IX	
	LD	B,(IX+IDCODE)	
	XOR	A	
	CALL	SCONS	
	RET		
;
;B NI IDCODE WO IRERU
MSUB:			
SCONS:			
	PUSH	BC	
	LD	L,(IX+PATAL)	
	LD	H,(IX+PATAH)	
	LD	B,(IX+XPOS)	
	CALL	SETPAD	
;
	LD	D,(HL)	
	INC	HL	
	LD	E,(HL)	
	LD	C,(IX+YPOS)	
	CALL	SUBHL	
	POP	BC	
	LD	C,(IX+YPOS)	
	CALL	SETSUB	
	RET		
;
;INPUT A:BACKCODE,B:IDCODE,C:YPOS,HL:SUBADR,DE:XYSIZE(BYTE)
SETSUB:			
	BIT	1,E	
	JR	NZ,SBB50	
	BIT	0,C	
	JR	Z,SBB50	
	INC	E	
	INC	E	
	INC	E	
	INC	E	
SBB50:			
	LD	C,A	
	XOR	A	
	EX	AF,AF'	
SSB100:			
	PUSH	DE	
	PUSH	HL	
SSB200:			
	LD	A,(HL)	
	CP	C	
	JR	Z,SSB300	
	BIT	7,A	
	JR	NZ,SSB300	
	EX	AF,AF'	
	INC	A	
	EX	AF,AF'	
SSB300:			
	LD	(HL),B	
	INC	HL	
	DEC	D	
	JR	NZ,SSB200	
	POP	HL	
	LD	DE,64	
	ADD	HL,DE	
	POP	DE	
	LD	A,E	
	SUB	4	
	LD	E,A	
	BIT	7,E	
	JR	NC,SSB100	
	EX	AF,AF'	
	OR	A	
	RET		
;
;
;
;
;
SETPAD:			
	BIT	0,B	
	JR	NZ,SPD10	
	INC	HL	
	INC	HL	
	RET		
SPD10:			
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
	RET		
;
;
XAKI:			
	PUSH	DE	
	LD	IX,XTBL	
	LD	DE,STEP	
XA10:	LD	A,(IX+0)	
	OR	A	
	JR	Z,XA20	
	INC	A	
	JR	Z,XA90	
	ADD	IX,DE	
	JR	XA10	
XA20:	DEC	A	
XA90:			
	POP	DE	
	RET		
;
SHPMSL:			
	LD	IY,XTBL	
	LD	A,(IY+IDCODE)	
	CP	2	
	RET	NZ	
	LD	IY,(UMSLA)	
	LD	E,0-3	
	LD	D,0	
	LD	A,(UMSLF)	
	CPL		
	LD	(UMSLF),A	
	OR	A	
	CALL	Z,SHPMSS	
	LD	(UMSLA),IY	
;
	LD	IY,(DMSLA)	
	LD	DE,6	
	LD	A,(DMSLF)	
	CPL		
	LD	(DMSLF),A	
	OR	A	
	CALL	Z,SHPMSS	
	LD	(DMSLA),IY	
;
	RET		
;
SHPMSS:			
;MEMO +16 MO INIT SIYOUNE!
	LD	B,5	
SSS50:			
	PUSH	BC	
SSS100:			
	LD	C,(IY+0)	
	LD	B,(IY+1)	
	LD	A,C	
	AND	B	
	INC	A	
	JR	NZ,SSS200	
	LD	C,(IY+2)	
	LD	B,(IY+3)	
	PUSH	BC	
	POP	IY	
	JR	SSS100	
SSS200:			
	IN	A,(C)	
	CPL		
	AND	B	
	JR	NZ,SSSFIR	
SSS300:			
	LD	BC,10	
	ADD	IY,BC	
	POP	BC	
	DJNZ	SSS50	
	RET		
;
SSSFIR:			
	CALL	XAKI	
	JR	Z,SSS300	;AKI NASHI
;SCRA,SUBA
	PUSH	IY	
	LD	IY,XTBL	
	LD	B,(IY+XPOS)	
	LD	C,(IY+YPOS)	
	INC	B	
	INC	B	
	POP	IY	
	LD	A,B	
	ADD	A,D	
	LD	B,A	
	LD	A,C	
	ADD	A,E	
	LD	C,A	
;JR C,SSS300
	LD	(IX+IDCODE),4	
	LD	(IX+IDCODE+16),4	
	LD	(IX+XPOS),B	
	LD	(IX+SXPOS),B	
	LD	(IX+XPOS+16),B	
	LD	(IX+YPOS),C	
	LD	(IX+SYPOS),C	
	LD	(IX+YPOS+16),C	
	LD	A,(IY+2)	
	LD	(IX+DX),A	
	LD	A,(IY+3)	
	LD	(IX+DY),A	
	LD	A,(IY+4)	
	LD	(IX+REPCON),A	
	LD	C,(IY+5)	
	LD	B,0	
	LD	HL,0A104H	
	ADD	HL,BC	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	LD	(IX+PATAL),E	
	LD	(IX+PATAH),D	
	LD	(IX+PATAL+16),E	
	LD	(IX+PATAH+16),D	
	LD	C,(IY+6)	
	LD	B,0	
	LD	HL,0A104H	
	ADD	HL,BC	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	LD	(IX+SMOKAL),E	
	LD	(IX+SMOKAH),D	
;
	LD	A,(IY+7)	
	LD	(IX+DX+16),A	
	LD	A,(IY+8)	
	LD	(IX+DY+16),A	
	LD	A,(IY+9)	
	LD	(IX+REPCON+16),A	
;
	LD	DE,10	
	ADD	IY,DE	
;
;DUMMY POP
	POP	AF	
	RET		
;
;
;
;
SMTBLU:			
STB1:			
	DEFB	4	
	DEFB	10H	
	DEFB	1	
	DEFB	0	
	DEFB	6	
	DEFB	10	
	DEFB	30	
	DEFB	0	;0FFH
	DEFB	0	
	DEFB	55H	
STB2:			
	DEFB	4	
	DEFB	128	
	DEFB	0FFH	
	DEFB	0FFH	
	DEFB	3	
	DEFB	16	
	DEFB	36	
	DEFB	0	;1
	DEFB	1	
	DEFB	0FFH	
STB3:			
	DEFB	4	
	DEFB	4	
	DEFB	1	
	DEFB	0FFH	
	DEFB	3	
	DEFB	12	
	DEFB	32	
	DEFB	0	;0FFH
	DEFB	1	
	DEFB	0FFH	
STB4:			
	DEFB	2	
	DEFB	32	
	DEFB	0	
	DEFB	0FFH	
	DEFB	6	
	DEFB	14	
	DEFB	34	
	DEFB	0	
	DEFB	1	
	DEFB	0FFH	
STB5:			
	DEFB	4	
	DEFB	2	
	DEFB	0FFH	
	DEFB	0	
	DEFB	4	
	DEFB	18	
	DEFB	38	
	DEFB	1	
	DEFB	0	
	DEFB	55H	
;
	DEFW	0FFFFH	
	DEFW	SMTBLU	
;
SMTBLD:			
STB6:			
	DEFB	2	
	DEFB	64	
	DEFB	1	
	DEFB	1	
	DEFB	3	
	DEFB	24	
	DEFB	36	
	DEFB	0FFH	
	DEFB	0	
	DEFB	0	
STB7:			
	DEFB	2	
	DEFB	2	
	DEFB	0FFH	
	DEFB	0	
	DEFB	4	
	DEFB	18	
	DEFB	38	
	DEFB	1	
	DEFB	0	
	DEFB	55H	
STB8:			
	DEFB	2	
	DEFB	128	
	DEFB	1	
	DEFB	0	
	DEFB	6	
	DEFB	10	
	DEFB	30	
	DEFB	0	
	DEFB	0	
	DEFB	55H	
STB9:			
	DEFB	4	
	DEFB	8	
	DEFB	0FFH	
	DEFB	1	
	DEFB	3	
	DEFB	20	
	DEFB	32	
	DEFB	1	
	DEFB	0	;0FFH
	DEFB	0	
STB0:			
	DEFB	2	
	DEFB	16	
	DEFB	0	
	DEFB	1	
	DEFB	4	
	DEFB	22	
	DEFB	34	
	DEFB	0	
	DEFB	0	
	DEFB	0	
;
	DEFW	0FFFFH	
	DEFW	SMTBLD	
;
;
;
BOOMS:			
	LD	A,C	
	BIT	7,A	
	RET	NZ	
	LD	A,B	
	CP	119	
	RET	NC	
;
	PUSH	IX	
	CALL	XAKI	
	JR	Z,BOOMSE	; AKI NASHI
	RES	0,B	
	LD	(IX+IDCODE),20	
	LD	(IX+IDCODE+16),20	
	LD	(IX+XPOS),B	
	LD	(IX+YPOS),C	
	LD	(IX+XPOS+16),B	
	LD	(IX+YPOS+16),C	
	LD	(IX+REPCON),0FFH	
BOOMSE:			
	POP	IX	
	RET		
;
BOOMM:			
	LD	IX,XTBL	
BOM10:	LD	A,(IX+IDCODE)	
	CP	0FFH	
	RET	Z	
	CP	20	
	CALL	Z,BOOM	
	LD	DE,STEP	
	ADD	IX,DE	
	JR	BOM10	
;
BOOM:			
;
	CALL	SETBM	
	LD	A,(IX+REPCON)	
	CP	0FFH	
	JR	Z,BM30	
	CALL	SETBMP	
;
	LD	B,0	
	LD	A,(IX+IDCODE)	
	CALL	MSUB	
;
BM30:			
	INC	(IX+REPCON)	
	LD	A,(IX+REPCON)	
	CP	10	
	JR	Z,BM60	
	CALL	SETBMP	
	LD	B,(IX+IDCODE)	
	LD	A,0	
	CALL	MSUB	
BM60:			
	RET		
;
SETBM:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	RET		
;
SETBMP:			
	PUSH	HL	
	LD	HL,BMTBL	
	LD	A,(IX+REPCON)	
	LD	E,A	
	LD	D,0	
	ADD	HL,DE	
	LD	E,(HL)	
	LD	D,0	
	LD	HL,0A104H	
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	LD	(IX+PATAL),E	
	LD	(IX+PATAH),D	
	POP	HL	
	RET		
;
;
BMTBL:			
	DEFB	40	
	DEFB	42	
	DEFB	42	
	DEFB	44	
	DEFB	46	
	DEFB	48	
	DEFB	50	
	DEFB	50	
	DEFB	48	
	DEFB	52	
	DEFB	54	
;
BOOMDS:			
	CALL	DSET	
	LD	A,(IX+REPCON)	
	CP	10	
	PUSH	AF	
	CALL	NZ,DISPXX	
	POP	AF	
	RET	NZ	
	CALL	CLRSXX	
	LD	(IX+IDCODE),0	
	RET		
;
;
COUNT:			
	XOR	A	
	LD	(CNTS),A	
	LD	B,20	
	LD	C,23	
	LD	HL,KYORI	
COUNT0:			
	LD	A,(CNTS)	
	INC	A	
	LD	(CNTS),A	
	LD	A,(HL)	
	INC	A	
	LD	(HL),A	
	CP	10	
	PUSH	AF	
	JR	NZ,COUNT1	
	LD	(HL),0	
COUNT1:			
	POP	AF	
	RET	NZ	
	INC	HL	
	LD	A,B	
	SUB	3	
	LD	B,A	
	RET	C	
	JR	COUNT0	
;
DSPCNT:			
	LD	HL,KYORI	
	LD	BC,8020H+1400H	
	CALL	SETCUR	
	LD	A,(CNTS)	
	LD	D,A	
DCN1:			
	LD	A,(HL)	
	INC	HL	
	ADD	A,30H	
	CALL	PUTCHR	
	LD	A,(CURX)	
	SUB	8	
	LD	(CURX),A	
	DEC	D	
	JR	NZ,DCN1	
	RET		
;
CLDSTN:			
	LD	HL,KYORI	
	LD	B,10	
CLDS1:	LD	(HL),0	
	INC	HL	
	DJNZ	CLDS1	
	RET		
;
; OUTPUT GRAPHIC ALPHANUMERIC CHARACTORS
;
; BC=(X,Y)CHR POS
SETCUR:			
	PUSH	AF	
	LD	A,B	
	LD	(CURX),A	
	LD	A,C	
	LD	(CURY),A	
	POP	AF	
	RET		
;
CURY:	DEFB	0	
CURX:	DEFB	0	
;
PUTLIN:			
	LD	A,(HL)	
	INC	HL	
	OR	A	
	RET	Z	
	CALL	PUTCHR	
	JR	PUTLIN	
;
;
;TO CHANGE MSL TO SMOKE
SMOKEN:			
	LD	(IX+IDCODE),18	
	LD	A,(IX+XPOS)	
	ADD	A,(IX+DX+16)	
	LD	(IX+XPOS),A	
	LD	(IX+XPOS+16),A	
	LD	B,A	
	LD	A,(IX+YPOS)	
	ADD	A,(IX+DY+16)	
	LD	(IX+YPOS),A	
	LD	(IX+YPOS+16),A	
	LD	C,A	
	LD	A,(IX+SXPOS)	
	ADD	A,(IX+DX+16)	
	LD	(IX+SXPOS),A	
	LD	D,A	
	LD	A,(IX+SYPOS)	
	ADD	A,(IX+DY+16)	
	LD	(IX+SYPOS),A	
	LD	E,A	
;
	INC	D	
	INC	D	
	LD	A,B	
	ADD	A,2	
	CP	D	
	JR	C,SMN100	
	LD	A,(IX+DX)	
	NEG		
	LD	(IX+DX),A	
	LD	A,(IX+DY)	
	NEG		
	LD	(IX+DY),A	
	RET		
;
SMN100:			
	DEC	D	
	DEC	D	
	LD	A,D	
	SUB	(IX+DX)	
	LD	(IX+XPOS),A	
	LD	(IX+XPOS+16),A	
	LD	A,E	
	SUB	(IX+DY)	
	LD	(IX+YPOS),A	
	LD	(IX+YPOS+16),A	
	LD	A,B	
	SUB	(IX+DX)	
	LD	(IX+SXPOS),A	
	LD	A,C	
	SUB	(IX+DY)	
	LD	(IX+SYPOS),A	
;
	RET		
;
SMOKEM:			
	LD	IX,XTBL	
SMM10:			
	LD	A,(IX+IDCODE)	
	CP	0FFH	
	RET	Z	
	CP	18	
	CALL	Z,SMOKE	
	LD	DE,STEP	
	ADD	IX,DE	
	JR	SMM10	
;
SMOKDS:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	LD	E,(IX+REPCON+16)	
	CALL	SUBHL	
SDS100:			
	CALL	FDXDY	
	JR	NC,SDS900	
	LD	A,E	
	CPL		
	LD	E,A	
	LD	A,(HL)	
	CP	80H	
	JR	C,SDS800	
	CP	85H	
	JR	NC,SDS800	
	LD	(HL),0	
	PUSH	BC	
	LD	D,4	
SDS200:			
	PUSH	HL	
	PUSH	DE	
	PUSH	BC	
	LD	L,(IX+SMOKAL)	
	LD	H,(IX+SMOKAH)	
	CALL	SMKPTN	
	CALL	CLRSXX	
	POP	BC	
	POP	DE	
	POP	HL	
	LD	A,B	
	ADD	A,(IX+DX)	
	LD	B,A	
	LD	A,C	
	ADD	A,(IX+DY)	
	LD	C,A	
	DEC	D	
	JR	NZ,SDS200	
	POP	BC	
SDS800:			
	LD	A,B	
	CP	(IX+SXPOS)	
	JR	NZ,SDS100	
	LD	A,C	
	CP	(IX+SYPOS)	
	JR	NZ,SDS100	
SDS900:			
	RET		
;
;
ASCJIS:			
	DEFW	2340H	
	DEFW	212AH	
	DEFW	212BH	
	DEFW	2174H	
	DEFW	2170H	
	DEFW	2173H	
	DEFW	2175H	
	DEFW	216CH	
	DEFW	214AH	
	DEFW	21ABH	
	DEFW	2176H	
	DEFW	215CH	
	DEFW	2124H	
	DEFW	215DH	
	DEFW	2125H	
	DEFW	213FH	
SMOKE:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	CALL	SUBHL	
	LD	D,0	
SMK100:			
	CALL	FDXDY	
	JR	NC,SMK900	
	LD	A,(HL)	
	BIT	7,A	
	JR	Z,SMK800	
	DEC	A	
	LD	(HL),A	
	INC	D	
SMK800:			
	LD	A,B	
	CP	(IX+SXPOS)	
	JR	NZ,SMK100	
	LD	A,C	
	CP	(IX+SYPOS)	
	JR	NZ,SMK100	
SMK900:			
	LD	A,D	
	OR	A	
	RET	NZ	
	LD	(IX+IDCODE),0	
	RET		
;
PRSTR0:	RET	
;
;
;
;
;
;
ZZZZZZ:			
	END
ÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿ