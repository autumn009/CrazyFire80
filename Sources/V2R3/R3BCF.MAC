;        BASIC-CRAZY-FIRE(BCF) V2R3
;	Machine Independent Module
;	  OCT.3,1983
;          by Akie
;-------------------------------------
SUBTTL BASIC-CRAZY-FIRE V2R3
.Z80
;
	EXT	PIF
	EXT	SMTBLU,SMTBLD
	EXT	SUBSCR,XTBL,STACK
	EXT	INIGRS,INIBPT,INISCR
	EXT	DISPM,SENSE,ESC,PRSTR0
	EXT	SHPCON,SHPMSL,SETPAD,CONVXY
	EXT	SUBSIZ,XNUM,ERROR
	EXT	SETBMP,SMKNUM,BMTBL
	EXT	MAKCHR,SYSINI
	EXT	KIRAI,INTERS,BASE,DESTRO
	EXT	CRUISE,BATLSP,WMISL,TAKO
	EXT	BDISTD,BDISTU
	EXT	PTSHP1,PTSHP2
	EXT	PTSMR,PTSML,PTSMU,PTSMD
	EXT	PTSMRU,PTSMLU,PTSMLD,PTSMRD
	EXT	PTSKR,PTSKL,PTSKU,PTSKD
	EXT	PTSKRU,PTSKLU,PTSKLD,PTSKRD
	EXT	PTBMP,PTBM4,PTBM5
	EXT	PTBM6,PTBM7,PTBMK1
	ENTRY	START,FDXDY,MSUB
	ENTRY	TSTSUB
	ENTRY	KYORI,CNTS,SMOKEN,GENMSL
	ENTRY	STEPSW,BOOMS
	ENTRY	RND,SUBADR,SUBHL,XAKI
	ENTRY	UMSLA,UMSLF,DMSLA,DMSLF
	ENTRY	KIRFLG,INTFLG,COLSW
	ENTRY	BSAFLG,BSBFLG,BSCFLG,BSDFLG
	ENTRY	CHISTP,CHIUND,CHIFU,CHIFD
	ENTRY	MUTEKI,OVRFLG,ENEERR,RUNERR
	ENTRY	DRMFLG,TAKFLG,TAKCNT
	ENTRY	TAKERR,CHIOVR,CHIPAT,INTCNT
	ENTRY	SCENE,SCEFLG,INTADD,PAREA
;
	JP	SYSINI
	JP	MAA
	JP	PIF
;
DISPSW:	DEFB	1
STEPSW:	DEFB	1
MUTEKI:	DEFB	1
;
PAREA:
RUNCNT:	DEFW	0FFFFH
BSAFLG:	DEFB	1
BSBFLG:	DEFB	1
BSCFLG:	DEFB	1
BSDFLG:	DEFB	1
KIRFLG:	DEFB	0
INTFLG:	DEFB	0
INTADD:	DEFW	0
DRMFLG:	DEFB	0
TAKFLG:	DEFB	0
;
CHISTP:	DEFB	4
CHIWID:	DEFB	30
CHIPAT:	DEFB	5AH
	DEFB	0A5H
SCENE:	DEFB	123
;
;
CHIUND:	DEFB	86
CHIOVR:	DEFB	4
INTCNT:	DEFW	0FFFFH
TAKCNT:	DEFW	100
TAKERR:	DEFB	0
OVRFLG:	DEFB	0
RUNERR:	DEFB	0
KYORI:	DEFS	10
ENEERR:	DEFB	0
SCEFLG:	DEFB	1
COLSW:	DEFB	1
;
WK1:	DEFS	2
CNTS:	DEFS	1
UMSLF:	DEFS	1
UMSLA:	DEFS	2
DMSLF:	DEFS	1
DMSLA:	DEFS	2
;
;
STEP	EQU	22*2
HSTEP	EQU	STEP/2
;	
IDCODE	EQU	0	
SCRAL	EQU	1	
SCRAH	EQU	2	
SUBAL	EQU	3	
SUBAH	EQU	4	
YPOS	EQU	5	
XPOS	EQU	6	
SYPOS	EQU	7	
SXPOS	EQU	8	
DX	EQU	9	
DY	EQU	10	
REPCON	EQU	11	
PATAL	EQU	12	
PATAH	EQU	13	
SMOKAL	EQU	14	
SMOKAH	EQU	15	
ENERGL	EQU	16
ENERGH	EQU	17
MSLSL	EQU	18
MSLSH	EQU	19
MODEF	EQU	20
KILLF	EQU	21
;
;
START:			
;
; SCREEN INIT
;
	LD	A,(DISPSW)
	OR	A
	CALL	NZ,INIGRS
;
	XOR	A
	LD	(OVRFLG),A
	LD	(CHIFD),A
	LD	(CHIFU),A
;
	LD	HL,SUBSCR	
	LD	DE,SUBSCR+1	
	LD	BC,SUBSIZ-1	
	LD	(HL),0	
	LDIR		
;
;
	LD	IX,XTBL	
	LD	(IX+IDCODE),2	
;SCRA,SUBA
;
	LD	(IX+YPOS),40	
	LD	(IX+YPOS+HSTEP),40
	LD	(IX+XPOS),40	
	LD	(IX+XPOS+HSTEP),40
	LD	(IX+REPCON),0	
	LD	HL,120*2
	LD	(IX+ENERGL),L
	LD	(IX+ENERGH),H
	LD	HL,500
	LD	(IX+MSLSL),L
	LD	(IX+MSLSH),H
	LD	HL,PTSHP1	
	LD	(IX+PATAL),L	
	LD	(IX+PATAH),H	
	LD	(IX+PATAL+HSTEP),L	
	LD	(IX+PATAH+HSTEP),H	
	LD	(IX+KILLF),0
	LD	B,(IX+IDCODE)
	LD	A,0
	CALL	MSUB
;
	LD	BC,XNUM
	LD	B,C
	DEC	B
	DEC	B
	LD	DE,STEP	
SEC50:	ADD	IX,DE	
	LD	(IX+IDCODE),0
	LD	(IX+KILLF),0
	DJNZ	SEC50	
	LD	(IX+STEP),0FFH	;SET EOF MARK
;
	XOR	A	
	LD	(UMSLF),A	
	LD	(DMSLF),A	
	LD	HL,SMTBLU	
	LD	(UMSLA),HL	
	LD	HL,SMTBLD	
	LD	(DMSLA),HL	
;
;
	CALL	CLDSTN	
;
	LD	A,(DISPSW)
	OR	A
	JR	Z,MA
	CALL	INIBPT		;INIT BACK&CHIK PATTERN	
	CALL	INISCR
MAA::	LD	A,0FFH
	LD	(BDISTD),A
	LD	(BDISTU),A
	LD	(SCEFLG),A
	XOR	A
	LD	(CHIFU),A
	LD	(CHIFD),A
MA::	CALL	SCROLL
	CALL	CHCKIL
	CALL	CHIK	
	CALL	SMOKEM	
	CALL	SHPMSL
	CALL	MAKCHR
	CALL	CHARAS	
	CALL	BOOMM	
	CALL	COUNT	
;
	LD	A,(DISPSW)
	OR	A
	CALL	NZ,DISPM
;
	LD	HL,(RUNCNT)
	DEC	HL
	LD	A,L
	OR	H
	LD	(RUNCNT),HL
	JR	NZ,MA100
	LD	A,1
	LD	(RUNERR),A
MA100:	LD	A,(OVRFLG)
	OR	A
	JP	NZ,ESC
	LD	A,(RUNERR)
	LD	B,A
	LD	A,(ENEERR)
	OR	B
	JR	NZ,MAEE
	LD	A,(TAKERR)
	OR	A
	JR	Z,MA200
	LD	A,(MUTEKI)
	OR	A
	JR	Z,MAEE
	LD	HL,20
	LD	(TAKCNT),HL
	XOR	A
	LD	(TAKERR),A
MA200:	CALL	SENSE	;KEY SCAN FOR STOP
	JP	MA	
;
MAEE:	RET
;
SCROLL::
	LD	DE,SUBSCR	
	LD	HL,SUBSCR+1	
	LD	BC,SUBSIZ-1	
	LDIR		
	LD	HL,SUBSCR+63	
	LD	DE,64	
	LD	B,48	
	LD	A,0	
SCL10:	LD	(HL),A	
	ADD	HL,DE	
	DJNZ	SCL10	
;
	LD	IX,XTBL	
SCL50:	LD	A,(IX+IDCODE)	
	OR	A	
	JR	Z,SCLEE	
	INC	A	
	RET	Z	
;DEC (IX+SCRA)
;JR NZ,SCL60
;DEC (IX+SCRA+1)
;SCL60:
;DEC (IX+SUBA)
;JR NZ,SCL70
;DEC (IX+SUBA+1)
;SCL70:
	DEC	(IX+SXPOS)	
	DEC	(IX+SXPOS)	
	LD	A,(IX+XPOS)	
	SUB	2	
	LD	(IX+XPOS),A	
	JR	C,SCLE1	
	LD	A,(IX+XPOS+HSTEP)	
	SUB	2	
	LD	(IX+XPOS+HSTEP),A	
	JR	C,SCLE1	
	JR	SCLEE	
SCLE1:	LD	(IX+KILLF),0FFH
SCLEE:	LD	DE,STEP	
	ADD	IX,DE	
	JR	SCL50	
;
CHCKIL:	LD	IX,XTBL
CKL100:	LD	A,(IX+IDCODE)
	OR	A
	JR	Z,CKLEE
	INC	A
	RET	Z
	LD	A,(IX+KILLF)
	OR	A
	JR	Z,CKLEE
	LD	(IX+IDCODE),0
	LD	(IX+KILLF),0
CKLEE:	LD	DE,STEP
	ADD	IX,DE
	JR	CKL100
;
CHIK:	LD	A,(CHIFD)
	OR	A
	JR	NZ,CHI90
	LD	A,(CHIUND)	
	CP	80	
	JR	NC,CHIUP	
	CP	55
	JR	C,CHIDWN	
	CALL	RND	
	AND	1	
	JR	NZ,CHIDWN	
CHIUP:	LD	A,(CHISTP)	
	LD	B,A
	LD	A,(CHIUND)
	SUB	B
	JR	CHICC	
CHIDWN:	LD	A,(CHISTP)
	LD	B,A
	LD	A,(CHIUND)	
	ADD	A,B	
CHICC:	LD	(CHIUND),A	
;
CHI90:	LD	A,(CHIFU)
	OR	A
	JR	NZ,CHI99
	LD	A,(CHIOVR)
	OR	A
	JR	Z,CHI99
	LD	A,(CHIOVR)
	CP	14
	JR	C,CODWN
	LD	A,(CHIWID)
	LD	B,A
	LD	A,(CHIUND)
	SUB	B
	LD	B,A
	LD	A,(CHIOVR)
	CP	B
	JR	NC,COUP
	CALL	RND
	AND	2
	JR	NZ,COUP
CODWN:	LD	A,(CHIOVR)
	LD	B,A
	LD	A,(CHISTP)
	ADD	A,B
	JR	CO95
COUP:	LD	A,(CHISTP)
	LD	B,A
	LD	A,(CHIOVR)
	SUB	B
CO95:	LD	(CHIOVR),A
;
CHI99:	LD	B,127	
	LD	C,96-2	
	CALL	SUBADR	
	LD	DE,0-64	
CHI100:	LD	(IY+0),7FH	
	DEC	C	
	DEC	C	
	ADD	IY,DE	
	LD	A,(CHIUND)	
	CP	C	
	JR	C,CHI100	
	LD	A,(CHIOVR)
	OR	A
	RET	Z
	LD	B,127
	LD	C,0
	CALL	SUBADR
	LD	DE,64
CHI200:	LD	(IY+0),7FH
	INC	C
	INC	C
	ADD	IY,DE
	LD	A,(CHIOVR)
	CP	C
	JR	NC,CHI200
	RET		
;
CHIFD:	DEFB	0
CHIFU:	DEFB	0
;
CHARAS::
	LD	IX,XTBL	
CHR10:	LD	A,(IX+IDCODE)	
	OR	A	
	JR	Z,CHRE	
	CP	0FFH	
	RET	Z	
	LD	HL,CHRTBL	
	LD	E,A	
	LD	D,0	
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	LD	HL,CHRE	
	PUSH	HL	
	EX	DE,HL	
	JP	(HL)	
CHRE:	LD	DE,STEP	
	ADD	IX,DE	
	JR	CHR10	
CHRTBL::
	DEFW	ERROR	
	DEFW	SHPCON	
	DEFW	MISSIL
	DEFW	KIRAI	
	DEFW	BASE	
	DEFW	INTERS	
	DEFW	DESTRO	
	DEFW	CRUISE	
	DEFW	BATLSP	
	DEFW	RETURN	;SMOKE
	DEFW	RETURN	;BOOM
	DEFW	WMISL
	DEFW	TAKO
;
RETURN:	RET		
;
MISSIL:	LD	A,(IX+IDCODE+HSTEP)
	OR	A
	JR	NZ,MSL50
	CALL	SMOKEN
	RET
MSL50:	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	LD	(IX+XPOS+HSTEP),B	
	LD	(IX+YPOS+HSTEP),C	
	PUSH	BC	
	LD	B,0	
	LD	A,(IX+IDCODE)	
	CALL	MSUB	
	POP	BC	
;	JR 	Z,MISBOM
	CALL	SUBHL	
	LD	A,(IX+REPCON)	
	LD	D,(IX+DX)	
	LD	E,(IX+DY)	
MSL100:	PUSH	AF	
	PUSH	HL	
	PUSH	BC	
	LD	A,B	
	ADD	A,(IX+DX+HSTEP)	
	LD	B,A	
	LD	A,C	
	ADD	A,(IX+DY+HSTEP)	
	LD	C,A	
	CALL	SUBHL	
	LD	A,(HL)	
	OR	A	
	JR	Z,MSL120	
	BIT	7,A	
	JR	NZ,MSL120
	POP	BC
	POP	HL
	JR	MISBM0
;
MSL120:	CALL	SMKNUM
	LD	(HL),A
	POP	BC	
	POP	HL	
	CALL	DXDY	
	JR	NC,MSL800
	LD	A,(HL)	
	OR	A	
	JR	Z,MSL200	
	AND	80H	
	JR	Z,MISBM0
MSL200:	POP	AF
	DEC	A	
	JR	NZ,MSL100	
	LD	(IX+XPOS),B	
	LD	(IX+YPOS),C	
	LD	(IX+SUBAL),L	
	LD	(IX+SUBAH),H	
	LD	A,0	
	LD	B,(IX+IDCODE)	
	CALL	MSUB	
	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)
	JR	NZ,MISBOM	
	RET
;
MSL800:	POP	AF
MSL900:	LD	(IX+IDCODE+HSTEP),0	
	RET
;
MISBM0:	POP	AF
MISBOM:
;	LD	B,(IX+XPOS)	
;	LD	C,(IX+YPOS)	
	LD	(IX+XPOS),B
	LD	(IX+YPOS),C
	LD	A,C	
	SUB	6
	LD	C,A	
	LD	A,B	
	SUB	6
	LD	B,A	
	CALL	BOOMS	
;
	JP	MSL900	
;
; DXDY SUB:[SCREEN POS(IY)],SUB POS(HL)
;	   X(B),Y(C) wo DX(D),DY(E) ni kuwaeru
;  IF NC THEN DXDYERROR
DXDY:	LD	A,E	
	OR	A	
	JR	Z,DXDY20		
	BIT	7,A
	JR	Z,ADD120
SUB120:	LD	A,E
	NEG
SUBL:	BIT	0,C
	JR	NZ,SUBEE	
	 PUSH	DE	
	 LD	DE,0-64	
	 ADD	HL,DE	
	 POP	DE
SUBEE:	DEC	C
	DEC	A
	JR	NZ,SUBL
	JR	DXDY20
ADD120:	LD	A,E
ADDL:	BIT	0,C	
	JR	Z,ADDEE	
	PUSH	DE	
	LD	DE,64	
	ADD	HL,DE
	POP	DE
ADDEE:	INC	C
	DEC	A
	JR	NZ,ADDL
DXDY20:	LD	A,C
	CP	90	
	JR	NC,DXDYER	
	OR	A	
	JR	Z,DXDYER	
	LD	A,D	
	OR	A	
	JR	Z,DXDY30
	BIT	7,A
	JR	Z,ADD1
SUB1:	PUSH	DE
	LD	A,D
	NEG
SUB1L:	BIT	0,B
	JR	NZ,SUBLL
	DEC	HL
SUBLL:	DEC	B
	DEC	A
	JR	NZ,SUB1L
	POP	DE
	JR	DXDY30
ADD1:	PUSH	DE
ADD1L:	BIT	0,B
	JR	Z,ADDLL
	INC	HL
ADDLL:	INC	B
	DEC	D
	JR	NZ,ADD1L
	POP	DE
DXDY30:	LD	A,B
	OR	A	
	JR	Z,DXDYER	
;BIT 7,A
;JR NZ,DXDYER
	CP	4	
	JR	C,DXDYER	
;CP 124
	CP	122	
	JR	NC,DXDYER	
	RET		
DXDYER:	OR	A	
	RET		
;
FDXDY:			
	PUSH	DE	
	LD	D,(IX+DX)	
	LD	E,(IX+DY)	
	CALL	DXDY	
	POP	DE	
	RET		
;
RND:	PUSH	HL
	LD	A,(LSTRND)
	LD	H,5
	LD	A,R
	ADD	A,L
	LD	L,A
	ADD	A,(HL)
	LD	H,44H
	ADD	A,(HL)
	LD	H,45H
	ADD	A,(HL)
	LD	(LSTRND),A
	POP	HL
	RET
;
LSTRND:	DEFS	1	
;
;
SUBADR:			
	PUSH	AF	
	PUSH	HL	
	LD	IY,SUBSCR	
	LD	H,C	
	LD	L,0	
	RES	0,H	
	SRL	H	
	RR	L	
	SRL	H	
	RR	L	
	SRL	H	
	RR	L	
	EX	DE,HL	
	ADD	IY,DE	
SUBA10:			
	LD	D,0	
	LD	E,B	
	SRL	E	
	ADD	IY,DE	
	EX	DE,HL	
	POP	HL	
	POP	AF	
	RET		
;
SUBHL:			
	PUSH	IY	
	CALL	SUBADR	
	PUSH	IY	
	POP	HL	
	POP	IY	
	RET		
;
;
;B NI IDCODE WO IRERU
MSUB:	PUSH	BC	
	LD	L,(IX+PATAL)	
	LD	H,(IX+PATAH)	
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)
	CALL	SETPAD	
;
	LD	B,(HL)	
	INC	HL	
	LD	C,(HL)
	CALL	CONVXY
	LD	E,C
	LD	D,B
	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)	
	CALL	SUBHL
;	PUSH	AF
;	LD	A,C
;	SUB	96
;	JR	C,MSB100
;	SUB	E
;	NEG
;	LD	E,A
;MSB100:	POP	AF
	POP	BC
	CALL	SETSUB	
	RET		
;
;INPUT	A:BACKCODE,B:IDCODE
;	HL:SUBADR,DE:XYSIZE(BYTE)
SETSUB:			
	LD	C,A
	XOR	A	
	EX	AF,AF'	
SSB100:	PUSH	DE	
	PUSH	HL	
SSB200:	LD	A,(HL)	
	CP	C	
	JR	Z,SSB300	
	BIT	7,A	
	JR	NZ,SSB300	
	CP	7FH
	CALL	Z,SSBADD
	EX	AF,AF'	
	INC	A	
	EX	AF,AF'	
SSB300:	LD	(HL),B	
	INC	HL	
	DEC	D	
	JR	NZ,SSB200	
	POP	HL	
	LD	DE,64	
	ADD	HL,DE	
	POP	DE	
	DEC	E
	JR	NZ,SSB100	
	EX	AF,AF'	
	OR	A	
	RET		
;
SSBADD:	EX	AF,AF'
	ADD	A,10
	EX	AF,AF'
	RET
;
TSTSUB:	PUSH	BC	
	LD	L,(IX+PATAL)	
	LD	H,(IX+PATAH)	
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)
	CALL	SETPAD	
;
	LD	B,(HL)	
	INC	HL	
	LD	C,(HL)
	CALL	CONVXY
	LD	E,C
	LD	D,B
	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)	
	CALL	SUBHL
;	PUSH	AF
;	LD	A,C
;	SUB	96
;	JR	C,MST100
;	SUB	E
;	NEG
;	LD	E,A
;MST100:	POP	AF
	POP	BC
;INPUT	A:BACKCODE,B:IDCODE
;	HL:SUBADR,DE:XYSIZE(BYTE)
	LD	C,A
	XOR	A	
	EX	AF,AF'	
SST100:	PUSH	DE	
	PUSH	HL	
SST200:	LD	A,(HL)	
	CP	C	
	JR	Z,SST300	
	BIT	7,A	
	JR	NZ,SST300	
	EX	AF,AF'	
	INC	A	
	EX	AF,AF'	
SST300:	INC	HL	
	DEC	D	
	JR	NZ,SST200	
	POP	HL	
	LD	DE,64	
	ADD	HL,DE	
	POP	DE	
	DEC	E
	JR	NZ,SST100	
	EX	AF,AF'	
	OR	A	
	RET		
;
XAKI:	PUSH	DE	
	LD	IX,XTBL	
	LD	DE,STEP	
XA10:	LD	A,(IX+0)	
	OR	A	
	JR	Z,XA20	
	INC	A	
	JR	Z,XA90	
	ADD	IX,DE	
	JR	XA10	
XA20:	DEC	A	
XA90:	POP	DE	
	RET		
;
; GENERATE A MISSILE GENPOS(B,C),PARAMS([IY])
;      XPOS([IX]),IDCODE(A)
GENMSL:
;SCRA,SUBA
	LD	(IX+IDCODE),A	
	LD	(IX+IDCODE+HSTEP),A	
	LD	(IX+XPOS),B	
	LD	(IX+SXPOS),B	
	LD	(IX+XPOS+HSTEP),B	
	LD	(IX+YPOS),C	
	LD	(IX+SYPOS),C	
	LD	(IX+YPOS+HSTEP),C	
	LD	A,(IY+0)	
	LD	(IX+DX),A	
	LD	A,(IY+1)	
	LD	(IX+DY),A	
	LD	A,(IY+2)	
	LD	(IX+REPCON),A	
	LD	E,(IY+3)	
	LD	D,(IY+4)	
	LD	(IX+PATAL),E	
	LD	(IX+PATAH),D	
	LD	(IX+PATAL+HSTEP),E	
	LD	(IX+PATAH+HSTEP),D	
	LD	E,(IY+5)
	LD	D,(IY+6)
	LD	(IX+SMOKAL),E	
	LD	(IX+SMOKAH),D	
	LD	(IX+SMOKAL+HSTEP),E
	LD	(IX+SMOKAH+HSTEP),D
	LD	A,(IY+7)
	LD	(IX+DX+HSTEP),A	
	LD	A,(IY+8)	
	LD	(IX+DY+HSTEP),A	
	LD	B,(IX+IDCODE)
	LD	A,0
	CALL	MSUB
	RET		
;
;
BOOMS:	LD	A,C	
	CP	80	
	RET	NC	
	LD	A,B	
	CP	119	
	RET	NC	
	PUSH	IX	
	CALL	XAKI	
	JR	Z,BOOMSE	; AKI NASHI
	LD	(IX+IDCODE),20	
	LD	(IX+IDCODE+HSTEP),20	
	LD	(IX+XPOS),B	
	LD	(IX+YPOS),C	
	LD	(IX+XPOS+HSTEP),B	
	LD	(IX+YPOS+HSTEP),C	
	LD	(IX+REPCON),0FFH	
	LD	(IX+REPCON+HSTEP),0FFH
BOOMSE:	POP	IX
	RET
;
BOOMM:	LD	IX,XTBL	
BOM10:	LD	A,(IX+IDCODE)	
	CP	0FFH	
	RET	Z	
	CP	20	
	CALL	Z,BOOM	
	LD	DE,STEP	
	ADD	IX,DE	
	JR	BOM10	
;
BOOM:	LD	A,(IX+XPOS)	;THESE ARE
	CP	128-16		;OWN TO
	RET	NC		;PATTERN
	LD	A,(IX+REPCON)
	CP	10
	JR	NZ,BM10
	LD	(IX+KILLF),1
	RET
BM10:	LD	(IX+REPCON+HSTEP),A
	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)	
	LD	A,(IX+REPCON)	
	CP	0FFH	
	JR	Z,BM30	
	CALL	SETBMP
	LD	B,0	
	LD	A,(IX+IDCODE)	
	CALL	MSUB	
BM30:	INC	(IX+REPCON)	
	LD	A,(IX+REPCON)	
	CP	10	
	JR	Z,BM60	
	CALL	SETBMP
	LD	B,(IX+IDCODE)	
	LD	A,0	
	CALL	MSUB	
BM60:	RET		
;
COUNT:	XOR	A	
	LD	(CNTS),A	
	LD	B,20	
	LD	C,23	
	LD	HL,KYORI	
COUNT0:	LD	A,(CNTS)	
	INC	A	
	LD	(CNTS),A	
	LD	A,(HL)	
	INC	A	
	LD	(HL),A	
	CP	10	
	PUSH	AF	
	JR	NZ,COUNT1	
	LD	(HL),0	
COUNT1:	POP	AF	
	RET	NZ	
	INC	HL	
	LD	A,B	
	SUB	3	
	LD	B,A	
	RET	C	
	JR	COUNT0	
;
CLDSTN:	LD	HL,KYORI
	LD	B,10	
CLDS1:	LD	(HL),0	
	INC	HL	
	DJNZ	CLDS1	
	RET		
;
;
;TO CHANGE MSL TO SMOKE
SMOKEN:	LD	(IX+IDCODE),18	
	LD	A,(IX+XPOS)	
	ADD	A,(IX+DX+HSTEP)	
	LD	(IX+XPOS),A	
	LD	(IX+XPOS+HSTEP),A	
	LD	B,A	
	LD	A,(IX+YPOS)	
	ADD	A,(IX+DY+HSTEP)	
	LD	(IX+YPOS),A	
	LD	(IX+YPOS+HSTEP),A	
	LD	C,A	
	LD	A,(IX+SXPOS)	
	ADD	A,(IX+DX+HSTEP)	
	LD	(IX+SXPOS),A	
	LD	D,A	
	LD	A,(IX+SYPOS)	
	ADD	A,(IX+DY+HSTEP)	
	LD	(IX+SYPOS),A	
	LD	E,A	
;
	INC	D	
	INC	D	
	LD	A,B	
	ADD	A,2	
	CP	D	
	JR	C,SMN100	
	LD	A,(IX+DX)	
	NEG		
	LD	(IX+DX),A	
	LD	A,(IX+DY)	
	NEG		
	LD	(IX+DY),A	
	RET		
;
SMN100:			
	DEC	D	
	DEC	D	
	LD	A,D	
	SUB	(IX+DX)	
	LD	(IX+XPOS),A	
	LD	(IX+XPOS+HSTEP),A	
	LD	A,E	
	SUB	(IX+DY)	
	LD	(IX+YPOS),A	
	LD	(IX+YPOS+HSTEP),A	
	LD	A,B	
	SUB	(IX+DX)	
	LD	(IX+SXPOS),A	
	LD	A,C	
	SUB	(IX+DY)	
	LD	(IX+SYPOS),A	
;
	RET		
;
SMOKEM:			
	LD	IX,XTBL	
SMM10:			
	LD	A,(IX+IDCODE)	
	CP	0FFH	
	RET	Z	
	CP	18	
	CALL	Z,SMOKE	
	LD	DE,STEP	
	ADD	IX,DE	
	JR	SMM10	
;
SMOKE:	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	CALL	SUBHL	
	LD	D,0	
SMK100:	CALL	FDXDY	
	JR	NC,SMK900	
	LD	A,(HL)	
	BIT	7,A	
	JR	Z,SMK800	
	INC	D
	CP	82H
	JR	C,SMK200
	DEC	A	
	LD	(HL),A	
	JR	SMK800
SMK200:	LD	(HL),0
SMK800:	LD	A,B	
	CP	(IX+SXPOS)	
	JR	NZ,SMK100	
	LD	A,C	
	CP	(IX+SYPOS)	
	JR	NZ,SMK100	
SMK900:	LD	A,D	
	OR	A	
	RET	NZ	
	LD	(IX+KILLF),1
	RET		
;
ZZZZZZ:
	END
0000 "
; 6
"  00 "
" 0   "
"0    "
"0000 "
"0   0"
"0   0"
" 000 "
; 7
"00000"
"0   0"
"   0 "
"  0  "
"  0  "
"  0  "
"  0  "
; 8
" 000 "
"0   0"
"0   0"
" 000 "
"0   0"
"0   0"
" 000 "
; 9
" 000 "
"0   0"
"0   0"
" 0000"
"    0"
"   0 "
" 00  "
; :
"     "
" 00  "
" 00  "
"     "
" 00  "
" 00  "
"     "
; ;
"     "
" 00  "
" 00  "
"     "
" 00  "
"  0  "
" 0   "
; <
"   00"
"  0  "
" 0   "
"0    "
" 0   "
"  0  "
"   00"
; =
"     "
"     "
"0000