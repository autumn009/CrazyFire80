; BCF PART-B V2R2
;	by Akie
.Z80
	EXT	MSUB,BOOMS,TSTSUB
	EXT	SUBADR
	EXT	SETKRP,XAKI,RND
	EXT	GENMSL,BSMTBL
	EXT	XTBL,KIRFLG,BASFLG
	EXT	SETBSP,SETTKP
	EXT	TAKFLG,TAKCNT,TAKERR
	EXT	DRMFLG,INTFLG,INTCNT
	EXT	CHIUND,CHIFLG
	EXT	PRSTR0,ESC
	EXT	PTSML,PTSKL
	EXT	PTINTW,PTINTO,PTINTM
	EXT	BEEPON,BEEPOF
	ENTRY	KIRAI,INTERS,BASE
	ENTRY	DESTRO,CRUISE
	ENTRY	BATLSP,WMISL
	ENTRY	TAKO,ERROR
	ENTRY	MAKCHR
;
STEP	EQU	22*2
HSTEP	EQU	STEP/2
;	
IDCODE	EQU	0	
SCRAL	EQU	1	
SCRAH	EQU	2	
SUBAL	EQU	3	
SUBAH	EQU	4	
YPOS	EQU	5	
XPOS	EQU	6	
SYPOS	EQU	7	
SXPOS	EQU	8	
DX	EQU	9	
DY	EQU	10	
REPCON	EQU	11	
PATAL	EQU	12	
PATAH	EQU	13	
SMOKAL	EQU	14	
SMOKAH	EQU	15	
ENERGL	EQU	16
ENERGH	EQU	17
MSLSL	EQU	18
MSLSH	EQU	19
MODEF	EQU	20
KILLF	EQU	21
;
MAKCHR:	CALL	MAKBAS
	CALL	MAKINT
	CALL	MAKKIR
	CALL	MAKTAK
	CALL	MAKDRM
	RET
;
MAKKIR:	LD	A,(KIRFLG)
	OR	A
	RET	Z
	CALL	RND
	BIT	0,A
	RET	Z
	CALL	RND
	AND	07FH
	CP	80
	RET	NC
	CP	8
	RET	C
	LD	C,A
;	LD	A,(CHIUND)
;	SUB	10
;	CP	C
;	RET	C
	RES	0,C
	LD	B,128-8
	CALL	XAKI
	RET	Z	;AKI NASHI
	LD	(IX+IDCODE),6
	LD	(IX+IDCODE+HSTEP),6
	LD	(IX+XPOS),B
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS),C
	LD	(IX+YPOS+HSTEP),C
	CALL	RND
	AND	07H
	LD	(IX+REPCON),A
	CALL	RND
	AND	1
	LD	(IX+MODEF),A
	CALL	SETKRP
	LD	A,0
	CALL	TSTSUB
	JR	NZ,MKR10
	LD	A,0
	LD	B,(IX+IDCODE)
	CALL	MSUB
	RET
MKR10:	LD	(IX+IDCODE),0
	RET
;
KIRAI:	CALL	SETKRP
	LD	A,(IX+IDCODE)
	LD	B,A
	CALL	MSUB
	RET	Z
	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
	LD	(IX+KILLF),1
	RET
;
BDIST:	DEFB	0FFH
BPKEEP:	DEFW	0
MAKBAS:	LD	A,(BDIST)
	CP	0FFH
	JR	NZ,MBS000
	LD	A,(BASFLG)
	OR	A
	RET	Z
MBS000:	LD	A,(BDIST)
	CP	0FFH
	JR	Z,MBS100
	OR	A
	JR	Z,MBS200
	DEC	A
	LD	(BDIST),A
	RET
;
MBS100:	CALL	RND
	AND	07H
	RET	NZ
	LD	A,1
	LD	(CHIFLG),A
	LD	A,2
	LD	(BDIST),A
	LD	A,(CHIUND)
	AND	0FCH
	SET	1,A
	LD	(CHIUND),A
	ADD	A,2
	LD	C,A
	LD	B,128-6
	LD	(BPKEEP),BC
	RET
;
MBS200:	LD	A,0FFH
	LD	(BDIST),A
	XOR	A
	LD	(CHIFLG),A
	CALL	XAKI
	RET	Z
	LD	(IX+IDCODE),1
	PUSH	IX
	CALL	XAKI
	POP	IY
	LD	(IY+IDCODE),0
	RET	Z
	PUSH	IY
	LD	(IX+IDCODE),8
	LD	(IX+IDCODE+HSTEP),8
	LD	BC,(BPKEEP)
	LD	(IX+XPOS),B
	LD	(IX+YPOS),C
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS+HSTEP),C
	CALL	RND
	AND	1
	LD	(IX+REPCON),A
	CALL	SETBSP
	LD	B,(IX+IDCODE)
 	LD	A,0
	CALL	MSUB
	POP	IX
	LD	IY,BSMTBL
	LD	BC,(BPKEEP)
	LD	A,B
	SUB	(IY+0)
	LD	B,A
	LD	A,C
	SUB	(IY+1)
	LD	C,A
	INC	IY
	INC	IY
	LD	A,22
	CALL	GENMSL
	CALL	RND
	AND	0FH
	SUB	8
	LD	(IX+MODEF),A
	RET
;
WMISL:	LD	A,(IX+IDCODE)
	LD	B,A
	CALL	MSUB
	JR	Z,WMS100
	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
	LD	(IX+KILLF),1
	RET
WMS100:	LD	A,(IX+XPOS)
	ADD	A,(IX+MODEF)
	LD	IY,XTBL
	SUB	(IY+XPOS)
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	(IY+YPOS)
	SRL	A
	SRL	A	;A=A/4(BSM.SPEED)
	LD	C,A
	CP	B
	RET	C
	CALL	BEEPON
	LD	(IX+IDCODE),4
	LD	(IX+IDCODE+HSTEP),4
	CALL	BEEPOF
	RET
;
BASE:	LD	A,(IX+REPCON)
	LD	(IX+REPCON+HSTEP),A
	INC	A
	CP	3
	JR	NZ,BAS100
	XOR	A
BAS100:	LD	(IX+REPCON),A
	CALL	SETBSP
	LD	A,(IX+IDCODE)
	LD	B,A
	CALL	MSUB
	RET	Z
	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
	LD	(IX+KILLF),1
	RET
;
MAKTAK:	LD	A,(TAKFLG)
	OR	A
	RET	Z
	LD	HL,(TAKCNT)
	LD	A,L
	OR	H
	JR	Z,MTK100
	DEC	HL
	LD	(TAKCNT),HL
	RET
MTK100:	CALL	XAKI
	RET	Z	;AKINASHI
	LD	B,128-12
MTK200:	CALL	RND
	CP	80
	JR	NC,MTK200
	LD	C,A
	LD	A,24
	LD	(IX+IDCODE),A
	LD	(IX+IDCODE+HSTEP),A
	LD	(IX+XPOS),B
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS),C
	LD	(IX+YPOS+HSTEP),C
	CALL	RND
	LD	(IX+REPCON),A
	CALL	SETTKP
	LD	B,(IX+IDCODE)
	LD	A,0
	CALL	TSTSUB
	JR	NZ,MTK300
	LD	B,(IX+IDCODE)
	LD	A,0
	CALL	MSUB
	LD	A,1
	LD	(TAKERR),A
	RET
MTK300:	LD	(IX+IDCODE),0
	RET		
;
TAKO:	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS+HSTEP),C
	LD	A,(IX+REPCON)
	LD	(IX+REPCON+HSTEP),A
	CALL	SETTKP
	LD	A,(IX+IDCODE)
	LD	B,0
	CALL	MSUB
	JR	NZ,TAK900
	LD	A,(IX+XPOS)
	DEC	A
	CP	3
	JR	C,TAK800
	LD	(IX+XPOS),A
	LD	IY,XTBL
	LD	A,(IY+IDCODE)
	CP	2
	LD	A,(IX+YPOS)
	JR	NZ,TAK200
	CP	(IY+YPOS)
	JR	Z,TAK200
	JR	C,TAK100
	SUB	2
	JR	TAK200
TAK100:	ADD	A,2
TAK200:	LD	(IX+YPOS),A
	LD	A,(IX+REPCON)
	INC	A
	LD	(IX+REPCON),A
	CALL	SETTKP
	LD	B,(IX+IDCODE)
	LD	A,0
	CALL	MSUB
	JR	NZ,TAK900
	RET
TAK900:	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
TAK800:	LD	(IX+KILLF),1
	RET
;
MAKDRM:	LD	A,(DRMFLG)
	OR	A
	RET	Z
	CALL	RND
	BIT	0,A
	RET	Z
	CALL	RND
	AND	07FH
	CP	80
	RET	NC
	CP	8
	RET	C
	LD	C,A
	RES	0,C
	LD	B,128-8
	CALL	XAKI
	RET	Z	;AKI NASHI
	CALL	SUBADR
	LD	A,(IY+0)
	OR	A
	RET	NZ
	LD	A,4
	LD	IY,DRMTBL
	CALL	GENMSL
;	LD	A,0
;	CALL	TSTSUB
;	JR	NZ,MDR10
;	LD	A,0
;	LD	B,(IX+IDCODE)
;	CALL	MSUB
	RET
;MDR10:	LD	(IX+IDCODE),0
;	RET
;
DRMTBL:	DEFB	0-2
	DEFB	0
	DEFB	4
	DEFW	PTSML
	DEFW	PTSKL
	DEFB	0
	DEFB	0
;
MAKINT:	LD	A,(INTFLG)
	OR	A
	RET	Z
	LD	HL,(INTCNT)
	LD	A,L
	OR	H
	RET	Z
	CALL	RND
	CP	35
	RET	NC
	ADD	A,10
	ADD	A,A
	RES	1,A
	LD	B,128-8
	LD	C,A
	CALL	XAKI
	RET	Z
	LD	(IX+IDCODE),10
	LD	(IX+IDCODE+HSTEP),10
	LD	(IX+XPOS),B
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS),C
	LD	(IX+YPOS+HSTEP),C
	LD	DE,PTINTW
	LD	(IX+PATAL),E
	LD	(IX+PATAL+HSTEP),E
	LD	(IX+PATAH),D
	LD	(IX+PATAH+HSTEP),D
	LD	(IX+DX),0-4
	LD	(IX+DY),0
	LD	(IX+MODEF),1	;CARRYING
	CALL	RND
	AND	7
	LD	(IX+REPCON),A
	LD	A,0
	CALL	TSTSUB
	JR	NZ,MIT100
	LD	A,0
	LD	B,(IX+IDCODE)
	CALL	MSUB
	RET
MIT100:	LD	(IX+IDCODE),0
	RET
;
INTERS:	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)
	LD	E,(IX+PATAL)
	LD	D,(IX+PATAH)
	LD	(IX+XPOS+HSTEP),B
	LD	(IX+YPOS+HSTEP),C
	LD	(IX+PATAL),E
	LD	(IX+PATAH),D
	LD	A,(IX+IDCODE)
	LD	B,0
	CALL	MSUB
	JR	NZ,INTBOM
	CALL	INTDIR
	LD	D,(IX+DX)
	LD	E,(IX+DY)
	CALL	INTTRY
	JR	Z,INTGO
	LD	D,(IX+DX)
	LD	E,0
	CALL	INTTRY
	JR	Z,INTGO
	LD	D,0
	LD	E,(IX+DY)
	CALL	INTTRY
	JR	Z,INTGO
	LD	A,(IX+DY)
	NEG
	LD	E,A
	PUSH	DE
	LD	D,(IX+DX)
	CALL	INTTRY
	POP	DE
	JR	Z,INTGO
	LD	D,0
	CALL	INTTRY
;
INTGO:	CALL	INTMSL
	LD	A,0
	LD	B,(IX+IDCODE)
	CALL	MSUB
	JR	NZ,INTBOM
	RET
;
INTBOM:	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
	LD	HL,(INTCNT)
	DEC	HL
	LD	(INTCNT),HL
	LD	(IX+KILLF),1
	RET
;
INTTRY:	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)
	PUSH	BC
	LD	A,B
	ADD	A,D
	CP	120
	JR	NC,ITRYER
	LD	(IX+XPOS),A
	LD	A,C
	ADD	A,E
	CP	80
	JR	NC,ITRYER
	LD	(IX+YPOS),A
	LD	A,0
	CALL	TSTSUB
	POP	BC
	RET	Z
ITR100:	LD	(IX+XPOS),B
	LD	(IX+YPOS),C
	RET
;
ITRYER:	POP	BC
	OR	0FFH
	JR	ITR100
;
INTDIR:	LD	(IX+DY),0
	LD	A,(IX+DX)
	OR	A
	JP	M,IDR300
	LD	A,(IX+XPOS)
	CP	128-12
	JR	C,IDR100
	LD	(IX+KILLF),1
	LD	(IX+DX),0
	RET
IDR100:	LD	A,(XTBL+YPOS)
	CP	48
	LD	A,8
	JR	C,IDR200
	NEG
IDR200:	LD	(IX+DY),A
	RET
IDR300:	LD	A,(IX+XPOS)
	CP	55
	JR	NC,IDR400
	LD	A,(IX+DX)
	NEG
	LD	(IX+DX),A
IDR400:	LD	A,(XTBL+YPOS)
	LD	B,(IX+YPOS)
	REPT	3
	INC	B
	ENDM
	CP	B
	LD	A,4
	JR	NC,IDR200
	LD	A,0-4
	JR	IDR200
;
INTMSL:	LD	A,(IX+MODEF)
	OR	A
	RET	Z
	LD	A,(XTBL+IDCODE)
	CP	2
	RET	NZ
	LD	A,(XTBL+YPOS)
	AND	0F8H
	LD	B,A
	LD	A,(IX+YPOS)
	ADD	A,(IX+REPCON)
	AND	0F8H
	CP	B
	RET	NZ
	PUSH	IX
	CALL	XAKI
	PUSH	IX
	POP	IY
	POP	IX
	RET	Z
	LD	A,(IX+DX)
	OR	A
	JP	P,IMS100
	NEG
	LD	(IX+DX),A
IMS100:	LD	(IX+MODEF),0
	LD	DE,PTINTO
	LD	(IX+PATAL),E
	LD	(IX+PATAH),D
	LD	B,(IX+XPOS)
	LD	C,(IX+YPOS)
	REPT	4
	INC	C
	ENDM
	LD	A,4
	PUSH	IX
	PUSH	IY
	POP	IX
	LD	IY,IMSTBL
	CALL	GENMSL
	POP	IX
	RET
;
IMSTBL:	DEFB	0-2
	DEFB	0
	DEFB	4
	DEFW	PTINTM
	DEFW	PTSKL
	DEFB	2
	DEFB	0
;
DESTRO:
CRUISE:
BATLSP:
ERROR:	LD	HL,ERRMES
	CALL	PRSTR0
	JP	ESC
ERRMES:	DEFB	0DH,0AH
	DEFB	'Undefined charactor exist!'
	DEFB	07,00	
;
	END
IPDS:	LD	A,(IX+REPCON)
	CPL
	LD	(IX+REPCON),A
	CALL	SETSH	
	CALL	DISPXX	
	RET		
SHIPCL:	LD	A,(IX+REPCON-HSTEP)
	LD	(IX+REPCON),A
	CALL	SETSH
	CALL	CLRSXX
	RET
;
SETSH:	LD	A,(IX+REPCON)	
	OR	A	
	LD	DE,PTSHP1	
	JR	Z,SETSH1	
	LD	DE,PTSHP2	
SE