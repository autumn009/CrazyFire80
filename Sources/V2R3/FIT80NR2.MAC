; CRAZY-FIRE MODULE: FIT80 (FOR PC8001)        
;			V2 R2
;          BY Akie
;------------------------------------
.Z80
;
;
	ENTRY	SUBSCR,XTBL,STACK
	ENTRY	INIGRS,INIBPT,INISCR
	ENTRY	DISPM,SENSE,ESC,PRSTR0
	ENTRY	CONVXY,SETPAD,XNUM
	ENTRY	SYSINI
	ENTRY	BMTBL,SETBMP,SMKNUM
	ENTRY	SUBSIZ,SHPMSL,SHPCON
	ENTRY	SMTBLU,SMTBLD
	ENTRY	DISPSS,DISPXX,CLRSXX,DSET
	ENTRY	SETCUR,PUTCHR,PUTLIN
	ENTRY	BEEPON,BEEPOF
	EXT	START,SUBADR,SUBHL,XAKI
	EXT	FDXDY,ERROR,GENMSL,MSUB
	EXT	KYORI,CNTS,SMOKEN,RND
	EXT	SCENE,SCEFLG
	EXT	BOOMS
	EXT	STEPSW,MUTEKI,ENEERR
	EXT	UMSLA,UMSLF,DMSLA,DMSLF
	EXT	PTSHP1,PTSHP2
	EXT	PTSMR,PTSML,PTSMU,PTSMD
	EXT	PTSMRU,PTSMLU,PTSMLD,PTSMRD
	EXT	PTSKR,PTSKL,PTSKU,PTSKD
	EXT	PTSKRU,PTSKLU,PTSKLD,PTSKRD
	EXT	PTBMP,PTBM4,PTBM5
	EXT	PTBM6,PTBM7,PTBMK1,PTBMK2
	EXT	CGADR
	EXT	KIRDS,KIRCL,BASEDS,BASECL
	EXT	INTDS,INTCL,DSTDS,DSTCL
	EXT	CRSDS,CRSCL,BTLDS,BTLCL
	EXT	WMSLDS,WMSLCL,TAKDS,TAKCL
;
FALSE	EQU	0
TRUE	EQU	NOT FALSE
;HARD WARE TYPE
PC8001	EQU	FALSE
PC8801	EQU	TRUE
KOM980	EQU	FALSE
;SOFTWARE MODE
NBASIC	EQU	TRUE
CPM	EQU	FALSE
;
	IF	CPM
	IF	PC8001
BANK	EQU	0E7H
ROM	EQU	0
RAM	EQU	1
	ENDIF
;
	IF	PC8801
BANK	EQU	31H
ROM	EQU	4
RAM	EQU	6
	ENDIF
;
	IF	KOM980
BANK	EQU	78H
ROM	EQU	0
RAM	EQU	80H
	ENDIF
;
	ENDIF
;
	IF	NBASIC
REBOOT	MACRO
	ROMSUB	81H
	ENDM
VRAM2	EQU	0DC00H
BANK	EQU	0	;ALWAYS ROM MODE
ROM	EQU	0
RAM	EQU	0
	ENDIF
;
	IF	CPM
REBOOT	MACRO
	JP	0
	ENDM
VRAM2	EQU	08000H
	ENDIF
;
ROMSUB	MACRO	A
	CALL	ROMCAL
	DEFW	A
	ENDM
;
STEP	EQU	22*2
HSTEP	EQU	STEP/2
;	
IDCODE	EQU	0	
SCRAL	EQU	1	
SCRAH	EQU	2	
SUBAL	EQU	3	
SUBAH	EQU	4	
YPOS	EQU	5	
XPOS	EQU	6	
SYPOS	EQU	7	
SXPOS	EQU	8	
DX	EQU	9	
DY	EQU	10	
REPCON	EQU	11	
PATAL	EQU	12	
PATAH	EQU	13	
SMOKAL	EQU	14	
SMOKAH	EQU	15	
ENERGL	EQU	16
ENERGH	EQU	17
MSLSL	EQU	18
MSLSH	EQU	19
MODEF	EQU	20
KILLF	EQU	21
;
;
;MEMORY MAP DEFINISION
VRAM1	EQU	0F300H
XNUM	EQU	80
VRMSIZ	EQU	120*25
SUBSIZ	EQU	64*52
SUBSCR	EQU	VRAM2-SUBSIZ
XTBL	EQU	SUBSCR-STEP*XNUM
KEYBUF	EQU	XTBL-10
;
RMCWK	EQU	VRAM2+VRMSIZ
STACK	EQU	RMCWK+300
;
;
SYSINI:	IF	KOM980
	LD	A,0FFH
	OUT	(079H),A
	ENDIF
	IF	CPM
	  LD	SP,STACK
	ENDIF
 	CALL	START
	IF	CPM
	  JP	ESC
	ELSE
	  RET
	ENDIF
;
INIGRS:
	RET
;
INIBPT:
	XOR	A	
	LD	(BACKP1),A	
	LD	(BACKP2),A	
;
	LD	IX,CHIPAT	
	LD	(IX+0),5AH	
	LD	(IX+1),5AH	
	RET
;
BACKP1:	DEFB	0
BACKP2:	DEFB	0
CHIPAT:	DEFB	0
	DEFB	0
;
;
INISCR:
	LD	HL,VRAM1
	LD	(DRVRAM),HL
	LD	(DSVRAM),HL
	CALL	CRTC
	CALL	CLS
	LD	BC,8004H	
	CALL	SETCUR	
	LD	HL,MSG1	
	CALL	PUTLIN	
	LD	BC,800CH	
	CALL	SETCUR	
	LD	HL,MSG2	
	CALL	PUTLIN	
	LD	BC,8018H	
	CALL	SETCUR	
	LD	HL,MSG3	
	CALL	PUTLIN	
	LD	BC,8028H
	CALL	SETCUR
	LD	HL,MSG4
	CALL	PUTLIN
	LD	BC,8038H
	CALL	SETCUR
	LD	HL,MSG5
	CALL	PUTLIN
	LD	BC,9840H
	CALL	SETCUR
	LD	A,'%'
	CALL	PUTCHR
	LD	A,1
	LD	(MDSFLG),A
	LD	B,80H
	LD	C,96-8*2-4
	CALL	SETCUR
	LD	HL,MSG6
	CALL	PUTLIN
	LD	HL,VRAM1
	LD	DE,VRAM2
	LD	BC,VRMSIZ
	LDIR
	LD	HL,VRAM2
	LD	(DRVRAM),HL
	CALL	SCRCHG
	RET
;
MSG1:	DEFM	"CRAZY",00	
MSG2:	DEFM	" FIRE",00	
MSG3:	DEFM	"DIST.",00	
MSG4:	DEFB	'MISS.',0
MSG5:	DEFB	'ENERG',0
MSG6:	DEFB	'SCENE',0
;
CLS: 
; CLEAR KEYBUF BY BIT
	LD	HL,KEYBUF
	LD	B,10
CLS50:	LD	(HL),0
	INC	HL
	DJNZ	CLS50
;
	LD	A,88H
	LD	(0EA5BH),A
	LD	A,12
	ROMSUB	257H
	LD	IX,VRAM1+50H
	LD	B,25
CLS100:	LD	(IX+0),0
	LD	(IX+1),5	;0:REV&SEC
	LD	(IX+2),3
	LD	(IX+3),0F8H	;3:COLOR7&GR
	LD	(IX+4),4
	LD	(IX+5),078H	;4:COLOR3&GR
	LD	(IX+6),5
	LD	(IX+7),0	;5:NORMAL
	LD	(IX+8),10
	LD	(IX+9),078H	;10:COLOR3&GR
	LD	(IX+10),59
	LD	(IX+11),5	;59:REV&SEC
	LD	(IX+12),60
	LD	(IX+13),0F8H	;60:COLOR7&GR
	LD	(IX+14),61
	LD	(IX+15),098H	;61:COLOR4&GR
	LD	(IX+16),64
	LD	(IX+17),4	;64:REV
	LD	(IX+18),79
	LD	(IX+19),5	;79:REV&SEC
	LD	DE,120
	ADD	IX,DE
	DJNZ	CLS100
	LD	DE,0-120
	ADD	IX,DE
	LD	(IX+1),1	;LAST LINE:SECRET
	LD	(IX+7),1
	LD	(IX+11),1
	LD	(IX+17),1
	LD	(IX+19),1
	RET
;
COLCOD:	REPT	5
	ADD	A,A
	ENDM
	OR	18H
	RET
;	
CNDCOL:	LD	IY,XTBL
	LD	A,(IX+IDCODE)
	CP	2
	JR	Z,CCL100
	LD	A,7
	JR	CCL200
CCL100:	LD	L,(IX+ENERGL)
	LD	H,(IX+ENERGH)
	PUSH	HL
	LD	DE,80*2
	OR	A
	SBC	HL,DE
	LD	A,4
	POP	HL
	JR	NC,CCL200
	LD	DE,40*2
	OR	A
	SBC	HL,DE
	LD	A,6
	JR	NC,CCL200
	LD	A,2
CCL200:	CALL	COLCOD
	LD	B,25
	LD	IX,(DRVRAM)
	LD	DE,120
CCL300:	LD	(IX+95),A
	ADD	IX,DE
	DJNZ	CCL300
	RET
;
SETCOL:	LD	B,25
	LD	IX,(DRVRAM)
;	LD	IY,(DSVRAM)
	ADD	IX,DE
;	ADD	IY,DE
	LD	DE,120
SCL100:	LD	(IX+0),A
;	LD	(IY+0),A
	ADD	IX,DE
;	ADD	IY,DE
	DJNZ	SCL100
	RET
;
RUNCOL:	LD	A,(COLPOS)
	DEC	A
	DEC	A
	CP	5
	JR	NC,RCL200
	LD	A,(CURCOL)
	LD	DE,85
	CALL	SETCOL
;
	LD	A,58
	LD	(COLPOS),A
	LD	DE,88
	CALL	SETCOL
	LD	HL,(RCLP)
	LD	A,(HL)
	OR	A
	JR	NZ,RCL100
	LD	HL,RCLTBL
	LD	A,(HL)
RCL100:	INC	HL
	LD	(RCLP),HL
	CALL	COLCOD
	LD	(CURCOL),A
	LD	DE,89
	CALL	SETCOL
RCL140:	LD	HL,(DRVRAM)
	LD	BC,80
	ADD	HL,BC
	EX	DE,HL
	LD	HL,(DSVRAM)
	ADD	HL,BC
	EX	DE,HL
	LD	A,25
RCL150:	LD	BC,40
	LDIR
	LD	BC,120-40
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	DEC	A
	JR	NZ,RCL150
	RET
;
RCL200:	LD	(COLPOS),A
	LD	DE,88
	CALL	SETCOL
	RET
;
COLPOS:	DEFB	10
CURCOL:	DEFB	078H
RCLP:	DEFW	RCLTBL
RCLTBL:	DEFB	2
	DEFB	2
	DEFB	3
	DEFB	3
	DEFB	6
	DEFB	6
	DEFB	4
	DEFB	4
	DEFB	5
	DEFB	5
	DEFB	1
	DEFB	1
	DEFB	0
;
;DISPXX  HL:DRADR,DE:PATTERNADR
;               BC:XY COUNT
DISPXX:			
	PUSH	HL
	PUSH	DE
	LD	DE,(DRVRAM)
	ADD	HL,DE
	POP	DE
DXX100:	PUSH	BC
	PUSH	HL
DXX200:	LD	A,(DE)
	OR	(HL)
	LD	(HL),A
	INC	DE
	INC	HL
	DJNZ	DXX200
	POP	HL
	LD	BC,120
	ADD	HL,BC
	POP	BC
	DEC	C
	JR	NZ,DXX100
	POP	HL
	RET	
;
DISPSS:			
	PUSH	HL
	PUSH	DE
	LD	DE,(DRVRAM)
	ADD	HL,DE
	POP	DE
DSS100:	PUSH	BC
	PUSH	HL
DSS200:	LD	A,20H
	OUT	(40H),A
	LD	A,(DE)
	OR	(HL)
	LD	(HL),A
	INC	DE
	INC	HL
	DJNZ	DSS200
	XOR	A
	OUT	(40H),A
	POP	HL
	LD	BC,120
	ADD	HL,BC
	POP	BC
	DEC	C
	JR	NZ,DSS100
	POP	HL
	RET	
;
CLRSXX:	PUSH	BC
	PUSH	HL
	PUSH	DE
	LD	DE,(DRVRAM)
	ADD	HL,DE
	POP	DE
CXX100:	PUSH	BC
	PUSH	HL
CXX200:	LD	A,(DE)
	CPL
	AND	(HL)
	LD	(HL),A
	INC	DE
	INC	HL
	DJNZ	CXX200
	POP	HL
	LD	BC,120
	ADD	HL,BC
	POP	BC
	DEC	C
	JR	NZ,CXX100
	POP	HL
	POP	BC
	RET
;
CLRSSS:	PUSH	BC
	PUSH	HL
	PUSH	DE
	LD	DE,(DRVRAM)
	ADD	HL,DE
	POP	DE
CSS100:	PUSH	BC
	PUSH	HL
CSS200:	LD	A,20H
	OUT	(40H),A
	LD	A,(DE)
	CPL
	AND	(HL)
	LD	(HL),A
	INC	DE
	INC	HL
	DJNZ	CSS200
	XOR	A
	OUT	(40H),A
	POP	HL
	LD	BC,120
	ADD	HL,BC
	POP	BC
	DEC	C
	JR	NZ,CSS100
	POP	HL
	POP	BC
	RET
;
;
;
DRADR::	PUSH	DE
	LD	L,C
	SRL	L
	SRL	L
	LD	H,0
	LD	E,L
	LD	D,H
	REPT	4
	ADD	HL,HL
	ENDM
	OR	A
	SBC	HL,DE
	REPT	3
	ADD	HL,HL
	ENDM
	LD	E,B
	LD	D,0
	SRL	E
	ADD	HL,DE
	POP	DE
	RET
;
;
;
SCRTR:
	LD	HL,(DSVRAM)
	LD	DE,(DRVRAM)
	LD	A,25
	INC	HL
SCR10:	LD	BC,63
	LDIR
	PUSH	DE
	LD	DE,120-63 
	ADD	HL,DE
	EX	(SP),HL
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	DEC	A
	JR	NZ,SCR10
	RET
;
SCRCHG:	LD	HL,(DSVRAM)
	LD	DE,(DRVRAM)
	LD	(DSVRAM),DE
	LD	(DRVRAM),HL
SCG100:	IN	A,(40H)
	AND	20H
	JR	Z,SCG100	;WAIT FOR VRTC
SCG200:	IN	A,(40H)
	AND	20H
	JR	NZ,SCG200
	LD	A,E
	OUT	(066H),A
	LD	A,D
	OUT	(066H),A
	RET
;
;
DSVRAM::	DEFW	VRAM1
DRVRAM::	DEFW	VRAM2
;
DSPCHI:	LD	B,127
	LD	C,0
	CALL	DRADR
	LD	DE,(DRVRAM)
	ADD	HL,DE
	CALL	SUBADR
	LD	B,25
	LD	A,(BACKP1)
	LD	E,A
	LD	A,(CHIPAT)
	LD	D,A
DCH100:	LD	A,(IY+0)
	CP	7FH
	CALL	Z,DCHS1
	CALL	NZ,DCHS2
	AND	33H
	LD	(HL),A
	LD	A,(IY+64)
	CP	7FH
	CALL	Z,DCHS1
	CALL	NZ,DCHS2
	AND	0CCH
	OR	(HL)
	LD	(HL),A
	PUSH	DE
	LD	DE,120
	ADD	HL,DE
	LD	DE,64*2
	ADD	IY,DE
	POP	DE
	DJNZ	DCH100
	RET
;
DCHS1:	LD	A,D
	RET
DCHS2:	LD	A,E
RETURN:	RET
;
DSPCHR:	LD	IX,XTBL
DCR10:	LD	A,(IX+IDCODE)
	CP	0FFH
	JR	Z,DCR50
	LD	E,A
	LD	D,0
	LD	HL,DCRCLT
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	DE,HSTEP
	ADD	IX,DE
	LD	DE,DCR20
	PUSH	DE
	JP	(HL)
DCR20:	LD	DE,STEP-HSTEP
	ADD	IX,DE
	JR	DCR10
;
DCR50:	LD	IX,XTBL	
DCR100:	LD	A,(IX+IDCODE)	
	CP	0FFH	
	RET	Z	
	OR	A
	JR	Z,DCR900
	LD	A,(IX+KILLF)
	OR	A
	JR	NZ,DCR900
	LD	A,(IX+IDCODE)
	LD	E,A	
	LD	D,0	
	LD	HL,DCRDST
	ADD	HL,DE	
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
	LD	DE,DCR900	
	PUSH	DE	
	JP	(HL)	
DCR900:			
	LD	DE,STEP	
	ADD	IX,DE	
	JR	DCR100	
DCRCLT:
	DEFW	RETURN
	DEFW	SHIPCL
	DEFW	MSLCL
	DEFW	KIRCL
	DEFW	BASECL
	DEFW	INTCL
	DEFW	DSTCL
	DEFW	CRSCL
	DEFW	BTLCL
	DEFW	SMOKCL
	DEFW	BOOMCL
	DEFW	WMSLCL
	DEFW	TAKCL
DCRDST:
	DEFW	RETURN	
	DEFW	SHIPDS	
	DEFW	MSLDS	
	DEFW	KIRDS	
	DEFW	BASEDS	
	DEFW	INTDS
	DEFW	DSTDS	
	DEFW	CRSDS	
	DEFW	BTLDS	
	DEFW	SMOKDS	
	DEFW	BOOMDS	
	DEFW	WMSLDS
	DEFW	TAKDS
;
SHIPDS:	LD	A,(IX+REPCON)
	CPL
	LD	(IX+REPCON),A
	CALL	SETSH	
	CALL	DISPXX	
	RET		
SHIPCL:	LD	A,(IX+REPCON-HSTEP)
	LD	(IX+REPCON),A
	CALL	SETSH
	CALL	CLRSXX
	RET
;
SETSH:	LD	A,(IX+REPCON)	
	OR	A	
	LD	DE,PTSHP1	
	JR	Z,SETSH1	
	LD	DE,PTSHP2	
SETSH1:	LD	(IX+PATAL),E
	LD	(IX+PATAH),D
	CALL	DSET
	RET		
;
CALCAD:			
	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	CALL	DRADR	
	RET		
;
DSET:	CALL	CALCAD	
	PUSH	HL	
	LD	L,(IX+PATAL)	
	LD	H,(IX+PATAH)	
	BIT	1,C	
	JR	NZ,DSET10	
	INC	HL	
	INC	HL	
	JR	DSET20	
DSET10:			
	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
DSET20:	LD	B,(HL)	
	INC	HL	
	LD	C,(HL)	
	INC	HL	
	EX	DE,HL
	POP	HL	
	RET		
;
MSLCL:	CALL	DSET	
	CALL	CLRSXX	
	RET
;
MSLDS:	LD	A,(IX+IDCODE+HSTEP)
	OR	A
	RET	Z
	LD	A,(IX+IDCODE)	
	CP	4	
	CALL	Z,GENSMK	
	CALL	DSET	
	CALL	DISPXX	
	RET		
;
GENSMK:	LD	A,(IX+XPOS+HSTEP)	
	ADD	A,(IX+DX+HSTEP)	
	LD	B,A	
	LD	A,(IX+YPOS+HSTEP)	
	ADD	A,(IX+DY+HSTEP)	
	LD	C,A	
	LD	A,(IX+REPCON)
GSM60:	PUSH	AF
	LD	L,(IX+SMOKAL)	
	LD	H,(IX+SMOKAH)	
	PUSH	BC
	CALL	SUBADR
	LD	A,(IY+0)
	BIT	7,A
	JR	Z,GSM70
	CALL	SMKPTN
	CALL	DISPXX	
GSM70:	POP	BC
	LD	A,B	
	ADD	A,(IX+DX)	
	LD	B,A	
	LD	A,C	
	ADD	A,(IX+DY)	
	LD	C,A	
	POP	AF
	DEC	A	
	JR	NZ,GSM60
	RET		
;
SMKPTN:	CALL	SETPAD	
	LD	D,(HL)	
	INC	HL	
	LD	E,(HL)	
	INC	HL	
	PUSH	DE
	EX	DE,HL	
	CALL	DRADR
	POP	BC
 	RET
;
SETPAD:	BIT	1,C
	JR	NZ,SPD10	
	INC	HL	
	INC	HL	
	RET
SPD10:	LD	E,(HL)	
	INC	HL	
	LD	D,(HL)	
	EX	DE,HL	
	RET		
;
SETBMP:	PUSH	HL
	PUSH	DE
	LD	HL,BMTBL	
	LD	A,(IX+REPCON)	
	LD	E,A
	LD	D,0
	ADD	HL,DE	
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(IX+PATAL),E
	LD	(IX+PATAH),D
	POP	DE
	POP	HL
	RET		
;
;
BMTBL:	DEFW	PTBMP
	DEFW	PTBM4
	DEFW	PTBM5
	DEFW	PTBM6
	DEFW	PTBM6
	DEFW	PTBM7
	DEFW	PTBM7
	DEFW	PTBM7
	DEFW	PTBMK1
	DEFW	PTBMK2
	DEFW	PTBM7
BOOMCL:	LD	A,(IX+REPCON)
	INC	A
	RET	Z
	CALL	SETBMP
	CALL	DSET
	CALL	CLRSSS
	RET
BOOMDS:	LD	A,(IX+REPCON)
	INC	A
	RET	Z
	CALL	SETBMP
	CALL	DSET	
	LD	A,(IX+REPCON)	
	CP	10	
	CALL	NZ,DISPSS
	RET		
;
;
DSPCNT:	LD	HL,KYORI
	LD	B,80H+6*4
	LD	C,20H
	CALL	SETCUR	
	LD	A,(CNTS)
	LD	D,A
DCN1:	LD	A,(HL)	
	INC	HL	
	ADD	A,30H	
	CALL	PUTCHR	
	LD	A,(CURX)
	SUB	12
	LD	(CURX),A
	DEC	D	
	JR	NZ,DCN1	
	RET		
;
;
; OUTPUT GRAPHIC ALPHANUMERIC CHARACTORS
;
CURY:	DEFB	0
CURX:	DEFB	0
; BC=(X,Y)CHR POS
SETCUR:	LD	(CURY),BC
	RET		
;
PUTCHR:	PUSH	AF
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	IX
	PUSH	IY
	CP	20H
	CALL	C,PCHS10
	CP	60H
	CALL	NC,PCHS10
	SUB	20H
	LD	L,A
	LD	H,0
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
	LD	DE,CGADR
	ADD	HL,DE
	EX	DE,HL
	LD	BC,(CURY)
	CALL	DRADR
	PUSH	HL
	LD	BC,(DRVRAM)
	ADD	HL,BC
	PUSH	HL
	POP	IY
	POP	HL
	LD	BC,(DSVRAM)
	ADD	HL,BC
	PUSH	HL
	POP	IX
	LD	A,(DE)
	INC	DE
	LD	(IY+0),A
	LD	(IX+0),A
	LD	A,(DE)
	INC	DE
	LD	(IY+1),A
	LD	(IX+1),A
	LD	A,(DE)
	INC	DE
	LD	(IY+2),A
	LD	(IX+2),A
	LD	A,(DE)
	INC	DE
	LD	(IY+120),A
	LD	(IX+120),A
	LD	A,(DE)
	INC	DE
	LD	(IY+121),A
	LD	(IX+121),A
	LD	A,(DE)
	INC	DE
	LD	(IY+122),A
	LD	(IX+122),A
	LD	A,(CURX)
	ADD	A,6
	LD	(CURX),A
	POP	IY
	POP	IX
	POP	BC
	POP	DE
	POP	HL
	POP	AF
	RET
;
PCHS10:	LD	A,'#'
	RET
;
PUTLIN:	LD	A,(HL)
	INC	HL	
	OR	A	
	RET	Z	
	CALL	PUTCHR	
	JR	PUTLIN	
;
SMOKCL:	RET
SMOKDS:	LD	B,(IX+XPOS)	
	LD	C,(IX+YPOS)	
	LD	E,(IX+REPCON+HSTEP)	
	CALL	SUBHL	
SDS100:	CALL	FDXDY	
	JR	NC,SDS900	
	LD	A,E	
	CPL		
	LD	E,A	
	LD	A,(HL)	
	CP	80H	
	JR	C,SDS800	
	CP	85H	
	JR	NC,SDS800	
	PUSH	BC	
	LD	D,1	
SDS200:	PUSH	HL	
	PUSH	DE	
	PUSH	BC	
	LD	L,(IX+SMOKAL)	
	LD	H,(IX+SMOKAH)	
	CALL	SMKPTN	
	CALL	CLRSXX	
	POP	BC	
	POP	DE	
	POP	HL	
	LD	A,B	
	ADD	A,(IX+DX)	
	LD	B,A	
	LD	A,C	
	ADD	A,(IX+DY)	
	LD	C,A	
	DEC	D	
	JR	NZ,SDS200	
	POP	BC	
SDS800:	LD	A,B	
	CP	(IX+SXPOS)	
	JR	NZ,SDS100	
	LD	A,C	
	CP	(IX+SYPOS)	
	JR	NZ,SDS100	
SDS900:	RET		
;
;
CONVXY:
; TO CONVERT DISP SCREEN CHR POS TO 
;   SUBSCREEN CHRPOS
	SLA	C	;SY=DY*2,SX=DX
	RET
;
;
PRSTR0:	ROMSUB	52EDH
	RET	
SENSE:	IN	A,(9)
	CPL
	AND	81H
	RET	Z
ESC:	LD	A,19H
	ROMSUB	9D7H
	REBOOT
;
CRTC:	LD	HL,CRTCT
	LD	DE,0EA5AH
	LD	BC,12
	LDIR
	LD	B,0
	LD	C,0FFH
	ROMSUB	08F7H
	LD	A,88H
	LD	(0EA5BH),A
	LD	BC,5019H
	ROMSUB	93AH
	CALL	CLS
	RET
;
CRTCT:	DEFB	0	;NULL CHARACTOR
	DEFB	0	;ATTRIBUTE CODE
	DEFB	50H	;F.KEY REVERSE
	DEFB	24	;SCROLL LENGTH
	DEFB	1	;SCROLL TOP
	DEFB	1	;CURSOR SW
	DEFB	1	;F.KEY FLAG
	DEFB	0	;COLOR MODE SW
	DEFB	19H	;VWIDTH
	DEFB	24	;CURSOR.Y
	DEFB	8	;CURSOR.X
	DEFB	50H	;HWIDTH
;
CONIN:	ROMSUB	0F75H
	RET
;
ROMCAL:
	EX	(SP),HL
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(RMCAD+1),DE
	POP	DE
	EX	(SP),HL
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	HL,RMCSUB
	LD	DE,RMCWK
	LD	BC,RMCSE-RMCSUB
	LDIR
	POP	BC
	POP	DE
	POP	HL
	JP	RMCWK
;
RMCSUB:	PUSH	AF
	LD	A,ROM
	OUT	(BANK),A
	POP	AF
RMCAD:	CALL	0
	PUSH	AF
	LD	A,RAM
	OUT	(BANK),A
	POP	AF
	RET
RMCSE:
;
;
DISPM:	CALL	SCRTR
	CALL	DSPCHI
	CALL	DSPCHR
	CALL	DSPCNT
	CALL	DSPENE
	CALL	DSPSMS
	CALL	DSPSCE
	CALL	CNDCOL
	CALL	RUNCOL
	CALL	SCRCHG
	LD	A,(STEPSW)
	OR	A
	CALL	Z,CONIN
	RET
;
;
;
SHPCON:	LD	IX,XTBL	
	LD	A,(IX+XPOS)	
	LD	(IX+XPOS+HSTEP),A	
	LD	A,(IX+YPOS)	
	LD	(IX+YPOS+HSTEP),A	
	INC	(IX+XPOS)	
	INC	(IX+XPOS)	
	LD	A,(IX+IDCODE)	
	CP	2	
	RET	NZ	
;
	IN	A,(1)	
	XOR	0FFH	
	JR	Z,SCON20	
	LD	A,(IX+YPOS)	
	CP	8	
	JR	C,SCON20	
	DEC	(IX+YPOS)	
	DEC	(IX+YPOS)
SCON20:	IN	A,(0)	
	XOR	0FFH	
	JR	Z,SCON50	
	LD	A,(IX+YPOS)	
	CP	80	
	JR	NC,SCON50	
	INC	(IX+YPOS)
	INC	(IX+YPOS)
SCON50:	LD	A,(IX+IDCODE)	
	PUSH	IX	
	LD	BC,HSTEP	
	ADD	IX,BC	
	LD	B,0	
	CALL	MSUB
	POP	IX
	CALL	SHPENE
	LD	B,(IX+IDCODE)	
	XOR	A	
	CALL	MSUB
	CALL	SHPENE
	RET	
;
SHPENE:	LD	E,A
	LD	D,0
	LD	L,(IX+ENERGL)
	LD	H,(IX+ENERGH)
	OR	A
	SBC	HL,DE
	JR	NC,SEN100
	LD	(IX+ENERGL),0
	LD	(IX+ENERGH),0
	LD	A,(MUTEKI)
	OR	A
	RET	NZ
	LD	A,(IX+XPOS)
	SUB	6
	LD	B,A
	LD	A,(IX+YPOS)
	SUB	6
	LD	C,A
	CALL	BOOMS
	LD	(IX+KILLF),1
	LD	A,1
	LD	(ENEERR),A
	RET
SEN100:	LD	DE,2
	ADD	HL,DE
	LD	DE,120*2
	PUSH	HL
	OR	A
	SBC	HL,DE
	POP	HL
	JR	C,SEN200
	LD	HL,120*2
SEN200:	LD	(IX+ENERGL),L
	LD	(IX+ENERGH),H
	RET
;
DSPENE:	LD	BC,8640H
	CALL	SETCUR
	LD	IX,XTBL
	LD	A,(IX+IDCODE)
	CP	2
	LD	HL,0
	JR	NZ,DEN100
	LD	L,(IX+ENERGL)
	LD	H,(IX+ENERGH)
	REPT	1	;2 BUNNO 1
	SRL	H
	RR	L
	ENDM
DEN100:	CALL	CVDEC
	INC	HL
	INC	HL
	LD	B,2
	LD	C,3
DEN200:	LD	A,(HL)
	CP	30H
	JR	NZ,DEN400
	LD	A,20H
	CALL	PUTCHR
	INC	HL
	DEC	C
	DJNZ	DEN200
DEN400:	LD	A,(HL)
	CALL	PUTCHR
	INC	HL
	DEC	C
	JR	NZ,DEN400
	RET
;
DSPSMS:	LD	A,(MDSFLG)
	OR	A
	RET	Z
	XOR	A
	LD	(MDSFLG),A
	LD	BC,8030H
	CALL	SETCUR
	LD	IX,XTBL
	LD	A,(IX+IDCODE)
	CP	2
	LD	HL,0
	JR	NZ,DSM100
	LD	L,(IX+MSLSL)
	LD	H,(IX+MSLSH)
DSM100:	CALL	CVDEC
	LD	B,4
	LD	C,5
	JR	DEN200
;
;
DSPSCE:	LD	A,(SCEFLG)
	OR	A
	RET	Z
	XOR	A
	LD	(SCEFLG),A
	LD	B,80H
	LD	C,96-8-4
	CALL	SETCUR
	LD	IX,XTBL
	LD	A,(SCENE)
	LD	L,A
	LD	H,0
	CALL	CVDEC
	LD	B,4
	LD	C,5
	JR	DEN200
;
CVBUF:	DEFS	5
; INPUT:HL=TARGET,USE AF,BC,DE,HL
; OUTPUT:[ HL ]
CV@АтЂтppтЂтрpт™тAњ Ѓњ‘рpт™тБњ њЂрpт™тБАppњ ™ЖGЗАт™тAГ’@GВppт™тБГ’@FBppт‘т „ рpт™тAњppт™тAДG“рpт™тC’АGВAћppтњrppтњrppтњrрpтђђЂт ™ЖGЗрpтђтрpтђтppт™т‘Бњ њЂрpтрpАppАOП’‡ЂА™@ћC€АЖGЗБЖGЗ0),HL=TARGET
;  OUTPUT:C=COUNTED(0-9),HL=LEFT RESULT
CVSUB:	LD	C,0
CVS100:	OR	A
	SBC	HL,DE
	JR	C,CVS200
	INC	C
	JR	CVS100
CVS200:	ADD	HL,DE
	RET
;
CVTBL:	DEFW	10000
	DEFW	1000
CVTBL2:	DEFW	100
	DEFW	10
	DEFW	1
;
;
SHPMSL: LD	HL,KEYBUF
	LD	B,10
	LD	C,0
SMS100:	IN	A,(C)
	CPL
	OR	(HL)
	LD	(HL),A
	INC	HL
	INC	C
	DJNZ	SMS100
;
	LD	IY,XTBL	
	LD	A,(IY+IDCODE)	
	CP	2	
	RET	NZ	
	LD	IY,(UMSLA)	
	LD	E,0-3	
	LD	D,0	
	LD	A,(UMSLF)	
	CPL		
	LD	(UMSLF),A	
	OR	A	
	CALL	Z,SHPMSS	
	LD	(UMSLA),IY	
	LD	IY,(DMSLA)	
	LD	DE,10
	LD	A,(DMSLF)	
	CPL
	LD	(DMSLF),A	
	OR	A	
	CALL	Z,SHPMSS	
	LD	(DMSLA),IY	
;
	RET		
;
SHPMSS:
;MEMO +HSTEP MO INIT SIYOUNE!
	LD	B,5
SSS50:	PUSH	BC	
SSS100:	LD	C,(IY+0)	
	LD	B,(IY+1)	
	LD	A,C	
	AND	B	
	INC	A	
	JR	NZ,SSS200	
	LD	C,(IY+2)	
	LD	B,(IY+3)	
	PUSH	BC	
	POP	IY	
	JR	SSS100	
SSS200:	LD	L,C
	LD	H,0
	PUSH	DE
	LD	DE,KEYBUF
	ADD	HL,DE
	POP	DE
	LD	A,B
	AND	(HL)
	JR	Z,SSS300
	LD	A,B
	CPL
	AND	(HL)
	LD	(HL),A
	JR	SSSFIR	
SSS300:	LD	BC,11
	ADD	IY,BC
	POP	BC
	DJNZ	SSS50	
	RET
;
MDSFLG:	DEFB	0
;
SSSFEE:
	LD	A,(MUTEKI)
	OR	A
	JR	NZ,SSF100
	POP	IY
	RET
;
SSSFIR:	CALL	XAKI	
	JR	Z,SSS300	;AKI NASHI
;SCRA,SUBA
	PUSH	IY	
	LD	IY,XTBL
	LD	L,(IY+MSLSL)
	LD	H,(IY+MSLSH)
	LD	A,L
	OR	H
	JR	Z,SSSFEE
	DEC	HL
SSF100:	LD	(IY+MSLSL),L
	LD	(IY+MSLSH),H
	LD	A,1
	LD	(MDSFLG),A
	LD	A,20H
	OUT	(40H),A
	LD	B,(IY+XPOS)	
	LD	C,(IY+YPOS)	
	INC	B	
	INC	B	
	POP	IY	
	LD	A,B	
	ADD	A,D	
	LD	B,A	
	LD	A,C	
	ADD	A,E	
	LD	C,A	
	PUSH	IY
	INC	IY
	INC	IY
	LD	A,4
	CALL	GENMSL
	POP	IY
	LD	BC,11
	ADD	IY,BC
	POP	BC	;DUMMY POP
	XOR	A
	OUT	(40H),A
	RET
;
;
;
;
SMTBLU:			
STB1:			
	DEFB	4	
	DEFB	10H
	DEFB	2	
	DEFB	0	
	DEFB	4	
	DEFW	PTSMR
	DEFW	PTSKR
	DEFB	0
	DEFB	0	
STB2:			
	DEFB	4	
	DEFB	128	
	DEFB	0FEH	
	DEFB	0FEH	
	DEFB	3	
	DEFW	PTSMLU
	DEFW	PTSKLU
	DEFB	0
	DEFB	0	
STB3:			
	DEFB	4	
	DEFB	4	
	DEFB	2	
	DEFB	0FEH	
	DEFB	3	
	DEFW	PTSMRU
	DEFW	PTSKRU
	DEFB	0
	DEFB	0	
STB4:			
	DEFB	2	
	DEFB	32	
	DEFB	0	
	DEFB	0FEH	
	DEFB	4	
	DEFW	PTSMU
	DEFW	PTSKU
	DEFB	0	
	DEFB	2	
STB5:			
	DEFB	4	
	DEFB	2	
	DEFB	0FEH	
	DEFB	0	
	DEFB	4	
	DEFW	PTSML
	DEFW	PTSKL
	DEFB	0
	DEFB	0	
;
	DEFW	0FFFFH	
	DEFW	SMTBLU	
;
SMTBLD:			
STB6:			
	DEFB	2
	DEFB	64
	DEFB	2
	DEFB	2
	DEFB	3
	DEFW	PTSMRD
	DEFW	PTSKRD
	DEFB	0
	DEFB	0
STB7:
	DEFB	2	
	DEFB	2	
	DEFB	0FEH	
	DEFB	0	
	DEFB	4	
	DEFW	PTSML
	DEFW	PTSKL
	DEFB	0	
	DEFB	0	
STB8:			
	DEFB	2	
	DEFB	128	
	DEFB	2	
	DEFB	0	
	DEFB	6	
	DEFW	PTSMR
	DEFW	PTSKR
	DEFB	0
	DEFB	0	
STB9:			
	DEFB	4	
	DEFB	8	
	DEFB	0FEH
	DEFB	2	
	DEFB	3	
	DEFW	PTSMLD
	DEFW	PTSKLD
	DEFB	2	
	DEFB	0FEH
STB0:			
	DEFB	2	
	DEFB	16	
	DEFB	0	
	DEFB	2	
	DEFB	4	
	DEFW	PTSMD
	DEFW	PTSKD
	DEFB	0	
	DEFB	0
;
	DEFW	0FFFFH	
	DEFW	SMTBLD	
;
SMKNUM:	CALL	RND
	AND	7
	ADD	A,85H
	RET
;
BEEPON:	PUSH	AF
	LD	A,20H
	OUT	(40H),A
	POP	AF
	RET
;
BEEPOF:	PUSH	AF
	XOR	A
	OUT	(40H),A
	POP	AF
	RET;
ZZZZZZ:			
	END	SYSINI
яяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяяя